#!/usr/bin/env zsh
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2013 by Cecil Curry.
# See "COPYING" for additional details.

declare_parcel_as_script_with_stdin <<'/---'
Handle *list getters* (i.e., functions getting list items and indices).
/---

# ....................{ GETTERS ~ item                     }....................
declare_function_with_stdin <<'/---'
string get_list_item(string list_name, integer index)

Get the element from the passed list with the passed (possibly negative) 1-based
index. If no such element exists, an exception is thrown: e.g.,

.get_list_item()
==========================================
[source]
------------------------------------------
>>> :list on_lived_meaning; on_lived_meaning=(
...     "Creating a life that reflects your values and satisfies your soul is a"
...     "rare achievement. In a culture that relentlessly promotes avarice and"
...     "excess as the good life, a person happy doing his own work is usually"
...     "considered an eccentric, if not a subversive. Ambition is only"
...     "understood if it's to rise to the top of some imaginary ladder of"
...     "success. Someone who takes an undemanding job because it affords him"
...     "the time to pursue other interests and activities is considered a"
...     "flake. A person who abandons a career in order to stay home and raise"
...     "children is considered not to be living up to his potential – as if a"
...     "job title and salary are the sole measure of human worth. You'll be"
...     "told in a hundred ways, some subtle and some not, to keep climbing,"
...     "and never be satisfied with where you are, who you are, and what"
...     "you're doing. There are a million ways to sell yourself out, and I"
...     "guarantee you'll hear about them. To invent your own life's meaning is"
...     "not easy, but it's still allowed, and I think you'll be happier for"
...     "the trouble." )
>>> get_list_item on_lived_meaning -4
you're doing. There are a million ways to sell yourself out, and I
------------------------------------------
==========================================
/---
function get_list_item() {
    # Validate sanity.
    die_unless_args_2 'Expected one list name and one list index.'
    string list_name__gli="${1}" list_index__gli="${2}"
    die_unless_list_index "${list_name__gli}" "${list_index__gli}"

    # Get such element.
    output_string "${${(@P)list_name__gli}[${list_index__gli}]}"
}

declare_function_with_stdin <<'/---'
string get_list_head(string list_name)

Get the first element from the passed nonempty list: e.g.,

.get_list_head()
==========================================
[source]
------------------------------------------
>>> list on_two_party_systems; on_two_party_systems=(
...    'I'll show you politics in America. Here it is, right here.'
...    '"I think the puppet on the right shares my beliefs."'
...    '"I think the puppet on the left is more to my liking."'
...    '"Hey, wait a minute, there's one guy holding out both puppets!"'
...    '"Shut up! Go back to bed, America. Your government is in control.'
...    'Here's "Love Connection." Watch this and get fat and stupid. By the'
...    'way, keep drinking beer, you fucking morons."' )
>>> get_list_head on_two_party_systems
I'll show you politics in America. Here it is, right here.
------------------------------------------
==========================================
/---
function get_list_head() {
    # Validate sanity.
    die_unless_arg 'Expected one list name.'
    string list_name__glh="${1}"
    die_unless_list_nonempty "${list_name__glh}"

    # Get such element.
    output_string "${${(@P)list_name__glh}[1]}"
}

declare_function_with_stdin <<'/---'
string get_list_tail(string list_name)

Get the first element from the passed nonempty list: e.g.,

.get_list_tail()
==========================================
[source]
------------------------------------------
>>> list on_american_culture; on_american_culture=(
...     "If you want to understand a society, take a good look at the drugs it"
...     "uses. And what can this tell you about American culture? Well, look at"
...     "the drugs we use. Except for pharmaceutical poison, there are"
...     "essentially only two drugs that Western civilization tolerates:"
...     "caffeine from Monday to Friday to energize you enough to make you a"
...     "productive member of society, and alcohol from Friday to Monday to"
...     "keep you too stupid to figure out the prison that you are living in." )
>>> get_list_tail on_american_culture
keep you too stupid to figure out the prison that you are living in.
------------------------------------------
==========================================
/---
function get_list_tail() {
    # Validate sanity.
    die_unless_arg 'Expected one list name.'
    string list_name__glt="${1}"
    die_unless_list_nonempty "${list_name__glt}"

    # Get such element.
    output_string "${${(@P)list_name__glt}[-1]}"
}

# ....................{ GETTERS ~ index                    }....................
#FIXME: Fix example.
declare_function_with_stdin <<'/---'
[stdout: integer, status: boolean] get_list_index_first_equaling_item_if_found(
    string list_name, string item)

Get the 1-based index of the first item in the passed list equal to the passed
string or return failure if no such item matches: e.g.,

.get_list_index_first_equaling_item_if_found()
==========================================
[source]
------------------------------------------
>>> list on_academia; on_academia=(
...    "There are but two ways of forming an opinion on science. One is the"
...    "scientific method; the other, the scholastic. One can judge from"
...    "experiment, or one can blindly accept authority. To the scientific"
...    "mind, experimental proof is all important and theory is merely a"
...    "convenience in description, to be junked when it no longer fits. To"
...    "the academic mind, authority is everything and facts are junked when"
...    "they do not fit theory laid down by authority. It is this point of view"
...    "– academic minds clinging like oysters to disproved theories – that has"
...    "blocked every advance of knowledge in history." )
>>> get_list_index_first_equaling_item_if_found\
...     on_academia "blocked every advance of knowledge in history."
9
------------------------------------------
==========================================
/---
function get_list_index_first_equaling_item_if_found() {
    # Validate sanity.
    die_unless_args_2 'Expected one list name and one search string.'
    string list_name__gliefif="${1}" list_item__gliefif="${2}"
    integer list_index__gliefif
    die_unless_list "${list_name__gliefif}"

    # 1-based index of the first matching item. Dismantled, this is:
    #
    # * '@P', expanding to the passed list.
    # * 'i', expanding to the index of the first matching item.
    # * 'e', interpreting such item as a string literal rather than glob.
    list_index__gliefif="${${(@P)list_name__gliefif}[(ie)${list_item__gliefif}]}"

    # Get such index if found or return failure.
    (( list_index__gliefif <= $(get_list_size "${list_name__gliefif}") )) and
        output_string "${list_index__gliefif}"
}

# --------------------( WASTELANDS                         )--------------------
#FUXME: Rename to get_list_index_first_equaling_item_if_found().
