#!/usr/bin/env zsh
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2013 by Cecil Curry.
# See "COPYING" for additional details.

declare_parcel_as_script_with_stdin <<'______________<heredoc?>______________'
Handle error, exception, and warning output.
______________<heredoc?>______________

#FIXME: The current overly simplistic get_print_message_prefix()-based approach
#should be jettisoned in favor of a Unicode box-drawing based approach: e.g.,
#
#/------------------------------[ zsh_gone_bad() ]-----------------------------\
#| An error has been thrown. It is a horrible error, but others are probably   |
#| worse.                                                                      |
#\-----------------------------------------------------------------------------/
#
#What's particularly nice about such an approach is that it inherently delimits
#prior from subsequent output. Indeed, we needn't even prefix such output with
#delimiting whitespace anymore, which is particularly nice.

# ....................{ PRINTERS ~ error                   }....................
declare_function_with_stdin <<'/---'
[stderr: string] print_error(string message1 = "", string message2, ...)

Print the passed strings (defaulting to the empty string) suffixed by a newline
and decorated by user-configured colors to standard error as an error message.
See print_message() for further details.
/---
function print_error() {
    start_stdout_printing_delimited
    print_string "${ZESHY_STYLE_TO_COLOR[print_error.caller]-}$(get_call_stack_root_basename)${ZESHY_STYLE_TO_COLOR[print_error.colon]-}: ${ZESHY_STYLE_TO_COLOR[print_error.message]-}${*}" 1>&2
}

declare_function_with_stdin <<'/---'
string print_error_exit_status(integer exit_status)

Print a description of the passed non-zero exit status to standard output. This
function is intended to be called only by die_with_exit_status(), whose
implementation guarantees state assumed by this function (e.g., a call stack
containing at least three callers).
/---
function print_error_exit_status() {
    # Validate passed arguments.
    die_unless_arg 'expected one exit status'
    string exit_status="${1}"
    die_unless_integer_nonnegative "${exit_status}"

    # Size of the current call stack, including the call to this function.
    integer call_stack_size; call_stack_size="$(get_call_stack_size)"

    # Caller's caller's name (i.e., the name of the function or script calling
    # the function or script calling this function). Assuming this function to
    # have been called by die_with_exit_status(), the current call stack
    # contains at least three and typically four stack frames:
    #
    # * ${funcstack[1]}, this function.
    # * ${funcstack[2]}, the die_with_exit_status() function.
    # * ${funcstack[3]}, the caller. This is typically either the
    #   intermediate zeshy-specific TRAPZERR() or die() functions.
    # * ${funcstack[4]}, the caller's caller. Assuming ${funcstack[2]} to be
    #   either TRAPZERR() or die(), this is the actual "caller."
    string caller_caller_name
    if is "${call_stack_size}" -ge 4 and
          "${funcstack[3]}" == ('TRAPZERR'|'die') si {
        caller_caller_name="${funcstack[4]}"
    } elif (( call_stack_size == 3 )) {
        caller_caller_name="${funcstack[3]}"
    } else {
        die "not called by die_with_exit_status()"
    }

    # Print such status. Avoid redirecting to standard error, as
    # die_with_exit_status() already does so.
    print_string "${ZESHY_STYLE_TO_COLOR[print_error.caller]-}${caller_caller_name}${ZESHY_STYLE_TO_COLOR[print_error.colon]-}: ${ZESHY_STYLE_TO_COLOR[print_error.message]-}failed with exit status ${exit_status}"
}

# ....................{ PRINTERS ~ warning                 }....................
declare_function_with_stdin <<'/---'
string print_warning(string message1 = "", string message2, ...)

Print the passed strings (defaulting to the empty string) suffixed by a newline
and decorated by user-configured colors to standard error as a warning message.
See print_message() for further details.
/---
function print_warning() {
    # Avoid calling "print -u2 -- "${message}". While such a command appears
    # superficially identical to the following command, passing the desired file
    # descriptor as an argument fails to preserve interleaving with prior
    # commands printing to file descriptor 1 (i.e., standard out).
    start_stdout_printing_delimited
    print_string "${ZESHY_STYLE_TO_COLOR[print_warning.caller]-}$(get_call_stack_root_basename)${ZESHY_STYLE_TO_COLOR[print_warning.colon]-}: ${ZESHY_STYLE_TO_COLOR[print_warning.message]-}${*}" 1>&2
}

# --------------------( WASTELANDS                         )--------------------
#declare_function_with_stdin <<'/---'
#string print_warning_sans_newline_suffix(
#    string message1 = "", string message2, ...)
#
#Print the passed strings (defaulting to the empty string) suffixed by no newline
#and decorated by user-configured colors to standard error as a warning message.
#See print_warning() for further details.
#/---
#function print_warning_sans_newline_suffix() {
#    print_string_sans_newline "$(print_warning "${@}" 2>&1)" 1>&2
#}
