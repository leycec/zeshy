#!/usr/bin/env zsh
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2013 by Cecil Curry.
# See "COPYING" for additional details.

declare_parcel_as_script_with_stdin <<'/---'
Handle *terminfo capabilities* (i.e., a map from human-readable terminal
operation names to the corresponding escape sequences under the current
terminal).
/---

#FIXME: Provide additional terminfo-based capabilities. See "man terminfo".

# ....................{ WRITERS                            }....................
declare_function_with_stdin <<'/---'
void clear_terminal(void)

Clear the current terminal of all prior output. This function implements a
pure-zsh analogue of the `clear` command.
/---
function clear_terminal() {
    # Validate sanity.
    die_if_args
    die_unless_stdout_terminal

    # Oddly, the terminfo "clear" capability moves the cursor to the second
    # rather than first line after clearing the screen. Correct this.
    echoti clear
    move_terminal_cursor_to 1 1
}

#FIXME: Rename to move_terminal_cursor_to_position().
#FIXME: Add support for negative index offsets.
declare_function_with_stdin <<'/---'
void move_terminal_cursor_to(integer column, integer row)

Move the cursor on the current terminal to the passed 1-based column and row
indices.
/---
function move_terminal_cursor_to() {
    # Validate sanity.
    die_unless_args_2 'Expected one column index and one row index.'
    die_unless_stdout_terminal
    string column="${1}" row="${2}"
    die_unless_integer_nonnegative "${column}"
    die_unless_integer_nonnegative "${row}"
    (( column <= $(get_terminal_width) )) or die\
        "Column ${column} exceeds terminal width $(get_terminal_width)."
    (( row <= $(get_terminal_height) )) or die\
        "Row ${row} exceeds terminal height $(get_terminal_height)."

    # Convert zsh-style 1-based indices to "terminfo"-style 0-based indices.
    echoti cup $(( row - 1 )) $(( column - 1 ))
}

# --------------------( WASTELANDS                         )--------------------
