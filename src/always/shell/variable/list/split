#!/usr/bin/env zsh
# ====================[ split                              ]====================
#
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2012 by Cecil Curry.
# See "COPYING" for additional details.
#
# --------------------( SYNOPSIS                           )--------------------
# Convert scalar strings to non-scalar lists.

# ....................{ SETTERS                            }....................
document_function '
string set_list_to_string_split_on_delimiter(
  string list_name, string text, character delimiter)

Split the passed string on all substrings of the passed delimiting character
into the passed list: e.g.,

    >>> list array
    >>> set_list_to_string_split_on_delimiter array "Obseqious/Deep/Join" "/"
    >>> print_string "${array[1]}"
    Obseqious
'
function set_list_to_string_split_on_delimiter() {
    # Validate passed arguments.
    die_unless_args_3\
        'expected one list name, one string, and one delimiter'
    string\
        list_name__sltssod="${1}"\
        text__sltssod="${2}"\
        delimiter__sltssod="${3}"
    die_unless_list "${list_name__sltssod}"
    die_unless_variable_character delimiter__sltssod

    # If the passed delimiter is a colon, delimit such delimiter by periods.
    if is "${delimiter__sltssod}" == ':' si; then
        set_list_to_evaluation "${list_name__sltssod}" '${(s.:.)text__sltssod}'
    # Otherwise, delimit such delimiter by colons.
    else
        set_list_to_evaluation "${list_name__sltssod}"\
            "\${(s:${delimiter__sltssod}:)text__sltssod}"
    fi
}

document_function '
string set_list_to_string_split_on_characters(string list_name, string text)

Split the passed string on characters into the passed list. Since Zsh supports
character indexing and slicing out of the box, this function is useful only
where explicitly requiring list types: e.g.,

    >>> list chars
    >>> set_list_to_string_split_on_characters chars\
    ...   "Though I sang in my chains like the sea."
    >>> print_string ${chars[1]} ${chars[31]} ${chars[39]}
    T e a
'
function set_list_to_string_split_on_characters() {
    # Validate passed arguments.
    die_unless_args_2 'expected one list name and one string'
    string list_name__sltssoc="${1}" text__sltssoc="${2}"
    die_unless_list "${list_name__sltssoc}"

    # Split with a 0-width delimiter.
    set_list_to_evaluation "${list_name__sltssoc}" '${(s::)text__sltssoc}'
}

# ....................{ SETTERS ~ word                     }....................
document_function '
void set_list_to_standard_input_split_on_words(string list_name)

Split standard input on shell words into the passed list.

    >>> list fern
    >>> set_list_to_standard_input_split_on_words fern <<< "About the lilting"
    >>> print_string ${fern[2]}
    the
'
function set_list_to_standard_input_split_on_words() {
    # Validate passed arguments.
    die_unless_arg 'expected one list name'
    string list_name__sltsisow="${1}"
    die_unless_list "${list_name__sltsisow}"
    die_if_standard_input_empty

    # Set such list and close standard input.
    set_list_to_evaluation "${list_name__sltsisow}" '${(z)"$(< /dev/stdin)"}'
    close_standard_input
}

document_function '
string set_list_to_string_split_on_words(string list_name, string text)

Split the passed string on words into the passed list. Since Zsh supports list-
style character indexing and slicing out of the box, this function is useful
only where explicit list types are required: e.g.,

    >>> list hill
    >>> set_list_to_string_split_on_words hill "Time held me green and dying"
    >>> print_string ${hill[1]} ${hill[4]} ${hill[5]}
    Time green dying
'
function set_list_to_string_split_on_words() {
    # Validate passed arguments.
    die_unless_args_2 'expected one list name and one string'
    string list_name__sltssow="${1}" text__sltssow="${2}"
    die_unless_list "${list_name__sltssow}"

    # Split with parameter expansion flag "(z)".
    set_list_to_evaluation "${list_name__sltssow}" '${(z)text__sltssow}'
}

# ....................{ SETTERS ~ string                   }....................
document_function '
void set_strings_to_string_split_on_delimiter(
  string variable_name1, string variable_name2, string text, string delimiter)

Split the passed string on the first substring of the passed delimiter into:

* The string with the former name to the substring preceding such delimiter.
* The string with the latter name to the substring following such delimiter.

The delimiter may be of any non-zero length: e.g.,

    >>> string pre suf
    >>> set_strings_to_string_split_on_delimiter pre suf "wol==hea==soc" "=="
    >>> print "prefix: ${pre}\nsuffix: ${suf}"
    prefix: wol
    suffix: hea==soc
'
function set_strings_to_string_split_on_delimiter() {
    # Validate passed arguments.
    die_unless_args_4\
        'expected one prefix variable name, one suffix variable name, one string, and one delimiter'
    string\
        prefix_name__sstspod="${1}"\
        suffix_name__sstspod="${2}"\
        text__sstspd="${3}"\
        delimiter__sstspd="${4}"
    die_unless_string "${prefix_name__sstspd}"
    die_unless_string "${suffix_name__sstspd}"
    die_unless_string_nonempty "${delimiter__sstspd}" 'no delimiter passed'

    # If such string contains no such delimiter, throw an exception.
    is_string_substring "${text__sstspd}" "${delimiter__sstspd}" or
        die "delimiter \"${delimiter__sstspd}\" not found in \"${text__sstspd}\""

    #FIXME: Conjoin into a single "//"-style match enabling "(#b)" to capture
    #the two groups, then set such strings with such groups. This also helps to
    #rid us of the unwieldy test above, assuming we detect such match failing
    #with non-zero exit status.
    #FIXME: Just use single "#" with "(#b)". See set_list_to_string_piped<>.
    # Split such string.
    set_string "${prefix_name__sstspd}" "${text__sstspd%%${delimiter__sstspd}*}"
    set_string "${suffix_name__sstspd}" "${text__sstspd#*${delimiter__sstspd}}"
}

# --------------------( WASTELANDS                         )--------------------
# string set_list_to_string_split_on_words(string list_name, string text)
#
# Split the passed string on words into the passed list. Since Zsh supports list-
# style character indexing and slicing out of the box, this function is useful
# only where explicit list types are required: e.g.,
#
#     >>> list words
#     >>> set_list_to_string_split_on_words words 'Time held me green and dying'
#     >>> print_string ${words[1]} ${words[4]} ${words[5]}
#     Time green dying
#function set_list_to_string_split_on_words() {
    # Validate passed arguments.
#    die_unless_args_2 'expected one list name and one string'
#    string list_name="${1}" __text__="${2}"
#    die_unless_list "${list_name}"

    # Split with parameter expansion flag "(z)".
#    set_list_to_evaluation "${list_name}" '${(z)__text__}'
#}
