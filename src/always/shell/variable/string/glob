#!/usr/bin/env zsh
# ====================[ glob                               ]====================
#
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2012 by Cecil Curry.
# See "COPYING" for additional details.
#
# --------------------( SYNOPSIS                           )--------------------
# Match and substitute Zsh-specific extended glob expressions.
#
# --------------------( SEE ALSO                           )--------------------
# Section "FILENAME GENERATION" of "man zshexpn"
#     Extended glob syntax reference.
#
# --------------------( TODO                               )--------------------
# * Also, note that glob expressions *CAN* match at the beginning and end of
#   strings: flags "s" and "e" in the same section as above. Between this and
#   backreferencing, I believe we've largely obsoleted our need for regular
#   expression matching in the Zeshy codebase and should probably substitute all
#   such matching with more efficient and certainly elegant glob matching.

# ....................{ TESTERS                            }....................
# boolean is_string_match(string text, string glob)
#
# Return true if the passed string matches the passed glob: e.g.,
#
#     >>> is_string_match 'youshima' '[fuk]ushima' or print 'dai-ichi'
#     dai-ichi
function is_string_match() {
    die_unless_two_args 'expected one string and one glob'
    is "${1}" == ${~2} si
}

# boolean is_string_containing_glob_characters(string text)
#
# Return true if the passed string contains at least one glob-reserved
# character and hence must be quote protected to be matched as is when globbing.
function is_string_containing_glob_characters() {
    die_unless_one_arg 'expected one string'
    is "${1}" == *[\[\]\<\>\(\)\|+*?#~^]* si
}

# ....................{ GETTERS                            }....................
# string get_glob_capturing_groups(string glob)
#
# Convert the passed glob to a glob capturing match groups (i.e., saving "(" and
# ")"-delimited match groups in global list ${match}), if not already.
function get_glob_capturing_groups() {
    # Validate passed arguments.
    die_unless_one_arg 'expected one glob'
    string glob="${1}"

    # If the passed glob contains no substring "(#b)" enabling match groups,
    # prefix such glob with such substring.
    is_string_containing "${glob}" '(#b)' or glob="(#b)${glob}"

    # And... we're good.
    output "${glob}"
}

# string get_glob_matching_lines(string glob)
#
# Convert the passed glob to a multiline glob (i.e., such that "^" and "$" also
# match newlines). While PCREs implicitly support multiline matching via option
# "-m", globs currently lack such support. Hence, this function is a fragile
# hack. Until such support improves, consider matching with multiline PCREs,
# instead.
function get_glob_matching_lines() {
    # Validate passed arguments.
    die_unless_one_arg 'expected one glob'
    string glob="${1}"

    # Replace all instances of "*" with "[^$'\n']#", thus terminating line
    # matching on the first newline.
    glob="${glob//\*/[^${ZESHY_CHARACTER_NEWLINE}]#}"
#   glob="${glob//\*/[^$'\n']#}"

    # Prefix such glob with an expression either matching at the start of the
    # string or the first newline preceding such match.
    glob="((#s)|*${ZESHY_CHARACTER_NEWLINE})${glob}*"
#   glob="((#s)|*$'\n')${glob}*"

    # And... we're done.
    output "${glob}"
}

# ....................{ GETTERS ~ group                    }....................
# string get_string_match(
#   string text, string glob, integer group_index = 1)
#
# Get the match group with the passed 1-based index (defaulting to the first
# such group) resulting from matching the passed string against the passed glob
# or throw an exception if such string fails to match: e.g.,
#
#     >>> get_string_match 'Escape from the zombie food court' '*(z[^ ]##)*'
#     zombie
function get_string_match() {
    get_string_match_if_found "${@}" or
        die "\"${text}\" not matched by glob \"${glob}\""
}

# string get_string_match_if_found(
#   string text, string glob, integer group_index = 1)
#
# Get the match group with the passed 1-based index (defaulting to the first
# such group) resulting from matching the passed string against the passed glob
# or return false if such string fails to match: e.g.,
#
#     >>> get_string_match_if_found 'Hologram' '*(o?)(o?)(o?)*' 2 or print 'ug'
#     ug
function get_string_match_if_found() {
    # Validate passed arguments.
    die_unless_two_or_three_args\
        'expected one string, one glob, and optional group index'
    string text="${1}" group_index="${3:-1}" glob
    die_unless_string_integer_positive "${group_index}"
    glob="$(get_glob_capturing_groups "${2}")"

    # Match such group. *AVOID* double-quoting such glob.
    if is "${text}" == ${~glob} si
    then output "${match[${group_index}]}"
    else return_false
    fi
}

# integer get_string_match_count(string text, string glob)
#
# Get the number of matches in the passed string of the passed glob: e.g.,
#
#     >>> get_string_match_count 'bosozoku' '[bsz]o'
#     3
function get_string_match_count() {
    # Validate passed arguments
    die_unless_two_args\
        'expected one string and one glob expression'
    string text="${1}" glob="${2}"

    # If either the passed string or glob expression is empty, return 0.
    if is_empty "${text}" or is_empty "${glob}"; then
        output 0
    #FIXME: Extract into a new get_substring_count() function.
    # If the passed glob expression contains no glob-reserved characters,
    # matching matches such expression as is. Thusly, the length of each match
    # is the length of such expression. The difference between the passed string
    # and a string with all such matches removed divided by the length of each
    # match yields the number of matches. Clever logic obscenely wins.
    elif not is_string_containing_glob_characters "${glob}"; then
        string text_matches_deleted="${text//${glob}/}"
        output $(( (${#text} - ${#text_matches_deleted}) / ${#glob} ))
    # Otherwise, count with brute-force iteration. While Zsh offers no flags or
    # functions for optimizing counting, iterating a counter while removing each
    # match until no matches remain suffices to yield the number of matches.
    else
        integer match_count=-1
        string text_old
        until is "${text_old}" == "${text}" si; do
            (( ++match_count ))
            text_old="${text}"
            text="${text/${~glob}/}"
        done
        output ${match_count}
    fi
}

# ....................{ REMOVERS                           }....................
# string remove_string_prefix_matching(string text, string glob)
#
# Remove the longest prefix from the passed string matching the passed glob and
# return the resulting string: e.g.,
#
#     >>> remove_string_prefix_matching 'north by northwest' '*n{o,d}?th'
#     west
function remove_string_prefix_matching() {
    die_unless_two_args 'expected one string and one glob'
    output "${1##${~2}}"
}

# string remove_string_suffix_matching(string text, string glob)
#
# Remove the longest suffix from the passed string matching the passed glob and
# return the resulting string: e.g.,
#
#     >>> remove_string_suffix_matching 'dial.m.for.murder' '.m?????*'
#     dial.
function remove_string_suffix_matching() {
    die_unless_two_args 'expected one string and one glob'
    output "${1%%${~2}}"
}

# --------------------( WASTELANDS                         )--------------------
# * Ah-ha! I finally found how to do proper matching with glob expressions:
#   flag "b". To quote, "Activate backreferences for parenthesised groups in the
#   pattern...". See "man zshexpn" for details. Sweetness.

    # Sadly, neither sed, awk, grep, or zsh itself have any coherent means of
    # matching individual occurences of a substring. Fortunately, zsh does have
    # a simple mechanism for *ELIMINATING* such matches. Hence, we do so
    # iteratively until all such matches are eliminated.
#FIXME: Ehrm; I'm quite sure there's a simple parameter expansion flag already
#implementing this. Man up "zshexpn", please. 

# Among other uses, this function counts occurrences of characters and substrings in strings#
# See also get_integer_of_matches(), similar but slightly more efficient by
# matching via one regular rather than multiple glob expressions.
