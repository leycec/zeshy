#!/usr/bin/env zsh
# ====================[ output                             ]====================
#
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2012 by Cecil Curry.
# See "COPYING" for additional details.
#
# --------------------( SYNOPSIS                           )--------------------
# Zeshy Shell Output, handling raw standard output and error.

# ....................{ PRINTERS                           }....................
# void sputter(string text1, string text2, ...)
#
# Print the passed strings to standard output with default trailing newline.
# Unlike utter(), print no theme-specific nonsense (e.g., ANSI color codes).
# Avoid calling echo() or print(), which fail on hyphen-prefixed strings: e.g.,
#
#     >>> string lammoth='-z' morgoth='rm -rf *'
#
#     # Bad. Never do this! Unless guaranteed no hyphens prefix such string.
#     # If "is ${1} == -* si", Zsh interprets the first word of such string as
#     # options to the "print" command. (Any user executing the following
#     # command is likely to frown and...and then what!?)
#     >>> print "${lammoth}" "${morgoth}"
#     >>> rm -rf *   # don't accidentally hit <Enter> here; what, you just did?
#
#     # Good. Always do this.
#     >>> sputter "${lammoth}" "${morgoth}"
#     -z rm -rf *    # that's more like it
sputter() {
    die_unless_args 'expected at least one string'
    print -r -- "${@}"
}

# void accurse(string text1, string text2, ...)
#
# Print the passed strings to standard error with default trailing newline.
# Unlike curse(), print no theme-specific nonsense (e.g., ANSI color codes).
accurse() {
    die_unless_args 'expected at least one string'
    print -r -- "${@}" 1>&2
}

# ....................{ PRINTERS ~ blank lines             }....................
# void sputter_blank_line(void)
#
# Print a blank line to standard output.
sputter_blank_line() {
    die_if_args
    print
}

# void accurse_blank_line(void)
#
# Print a blank line to standard error.
accurse_blank_line() {
    die_if_args
    print 1>&2
}

# ....................{ PRINTERS ~ escapes                 }....................
# void sputter_escaped(string text1, string text2, ...)
#
# Print the passed strings to standard output with default trailing newline and
# all escapes escaped (i.e., converted to the corresponding characters): e.g.,
#
#     >>> sputter_escaped "\nTHEY are rattling breakfast plates...\n"
#
#     THEY are rattling breakfast plates...
#
#     >>> sputter         "\nTHEY are rattling breakfast plates...\n"
#     \nTHEY are rattling breakfast plates...\n
sputter_escaped() {
    die_unless_args 'expected at least one string'
    print -- "${@}"
}

# void accurse_escaped(string text1, string text2, ...)
#
# Print the passed strings to standard error with default trailing newline and
# all escapes escaped, as with sputter_escaped().
accurse_escaped() {
    die_unless_args 'expected at least one string'
    print -- "${@}" 1>&2
}

# ....................{ PRINTERS ~ undelimited             }....................
# void sputter_undelimited(string text1, string text2, ...)
#
# Print the passed strings to standard output without delimitation: e.g.,
#
#     >>> sputter_undelimited "Where Alph," "the sacred river," ran
#     Where Alph,the sacred river,ran
sputter_undelimited() {
    # Temporarily set the ${IFS} global to the empty string. Since Zsh
    # implicitly delimits lists interpolated with the "*" operator with such
    # global, this prints lists undelimited. This was surprisingly hard to do.
    die_unless_args 'expected at least one string'
    string IFS=
    print -r -- "${*}"
}

# void accurse_undelimited(string text1, string text2, ...)
#
# Print the passed strings to standard error without delimitation: e.g.,
#
#     >>> accurse_undelimited "Through caverns" "measureless to" "man"
#     Through cavernsmeasureless toman
accurse_undelimited() {
    die_unless_args 'expected at least one string'
    string IFS=
    print -r -- "${*}" 1>&2
}

# ....................{ PRINTERS ~ nulls                   }....................
# void sputter_null_delimited(string text1, string text2, ...)
#
# Print the passed strings to standard output delimited and terminated by nulls
# (i.e., such that a null '\0' separates each such string from the next and the
# last such string is followed by a null '\0').
sputter_null_delimited() {
    die_unless_args 'expected at least one string'
    print -Nr -- "${@}"
}

# void accurse_null_delimited(string text1, string text2, ...)
#
# Print the passed strings to standard output delimited and terminated by nulls.
# See accurse_null_delimited for details.
accurse_null_delimited() {
    die_unless_args 'expected at least one string'
    print -Nr -- "${@}" 1>&2
}

# ....................{ PRINTERS ~ newlines                }....................
# void sputter_newline_delimited(string text1, string text2, ...)
#
# Print the passed strings to standard output delimited by newlines (i.e., such
# that a newline separates each such string from the next): e.g.,
#
#     >>> sputter_newline_delimited sludge elf transmuter
#     sludge
#     elf
#     transmuter
sputter_newline_delimited() {
    die_unless_args 'expected at least one string'
    print -lr -- "${@}"
}

# void sputter_newline_delimited_list(
#   string list_name, int start_index = 1, int end_index = -1)
#
# Print the elements between the passed start and end indices (defaulting to the
# first and last elements) of the list with the passed name to standard output
# delimited by newlines. If no indices are passed, all elements are printed; if
# one index is passed, only that index is passed. Negative indices are offset
# from the end: e.g.,
#
#     >>> list runes; runes=( abyss crypt pan tomb vaults )
#     >>> sputter_newline_delimited_list runes
#     abyss
#     crypt
#     pan
#     tomb
#     vaults
#     >>> sputter_newline_delimited_list runes 3
#     pan
#     >>> sputter_newline_delimited_list runes 3 -1
#     pan
#     tomb
#     vaults
sputter_newline_delimited_list() {
    # Validate.
    die_unless_one_to_three_args\
        'expected one variable name and optional start and end indices'
    string list_name="${1}" beg_index="${2:-}" end_index="${3:-}"
    check_list_args "${list_name}" beg_index end_index

    # Sputter.
#   print "beg: ${beg_index}\nend: ${end_index}"
    print -lr -- "${${(@P)list_name}[${beg_index},${end_index}]}"
}

# ....................{ PRINTERS ~ no newline              }....................
# void sputter_no_newline(string text1, string text2, ...)
#
# Print the passed strings to standard output with no default trailing newline.
sputter_no_newline() {
    die_unless_args 'expected at least one string'
    print -nr -- "${@}"
}

# void accurse_no_newline(string text1, string text2, ...)
#
# Print the passed strings to standard error with no default trailing newline.
accurse_no_newline() {
    die_unless_args 'expected at least one string'
    print -nr -- "${@}" 1>&2
}

# --------------------( WASTELANDS                         )--------------------
# * I'm strongly in favor of:
#   sputter() or stutter() or mutter()
#   accurse() or ???????()
#   Yeah, sputter() and accurse() read quite well.
