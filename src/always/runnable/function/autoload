#!/usr/bin/env zsh
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2013 by Cecil Curry.
# See "COPYING" for additional details.

declare_parcel_as_script_with_stdin <<'-/-'
Handle function autoloading.

== See Also ==

* Section ``AUTOLOADING FUNCTIONS'' of `man zshmisc`, reference documentation
  for function autoloading.
-/-

# ....................{ EXCEPTIONS                         }....................
declare_function_with_stdin <<'/---'
void die_unless_function_autoloadable(
    string function_name,
    string error_message = "\"${function_name}\" already defined or not found in the current \$FPATH")

Throw an exception with the passed message unless the passed function is
autoloadable. See is_function_autoloadable() for further details.
/---
function die_unless_function_autoloadable() {
    # Validate passed arguments.
    die_unless_args_1_to_2\
        'expected one function name and optional error message'
    string function_name="${1}"

    # For granular exception messages, test such conditions separately.
    is_function "${function_name}" and
        die "${2:-\"${function_name}\" already defined}"
    is_function_autoloadable "${function_name}" or
        die "${2:-\"${function_name}\" not found in the current \$FPATH}"
}

# ....................{ TESTERS                            }....................
declare_function_with_stdin <<'/---'
[status: boolean] is_function_autoloadable(string function_name)

Return success if the passed function is *autoloadable* (i.e., is not currently
defined and is in the current user's function path, typically in a file of the
same name in a directory in such path). See section ``AUTOLOADING FUNCTIONS'' of
`man zshmisc` for further details.
/---
function is_function_autoloadable() {
    # Dismantled, this is:
    #
    # * "run_command_silent", silencing errors autoload() prints on failure.
    # * "+X", returning true if autoloadable and false otherwise, in which case
    #   autoload() prints an error.
    # * "(q)", quote-protecting such name as run_command() requires.
    die_unless_arg 'expected one function name'
    run_command_silent autoload +X -- "${(q)1}"
}

# ....................{ AUTOLOADERS                        }....................
declare_function_with_stdin <<'/---'
void autoload_function(string function_name1, string function_name2, ...)

Autoload the passed previously undefined functions. Define each function to
autoload the actual definition for such function from the current user's
function path. See is_function_autoloadable() for further details.
/---
function autoload_function() {
    # Validate passed arguments.
    die_unless_args 'expected at least one function name'

    # Autoload such functions.
    for function_name ("${@}") {
        # Since "autoload -U" fails to return non-zero exit status if such
        # function is not autoloadable, do so before running such command.
        die_unless_function_autoloadable "${function_name}"

        # For safety, autoload such function under option "-U" to suppress alias
        # expansion. This prevents expansion of Zeshy- and user-specific aliases
        # under core zsh functions implemented with no such aliases in mind.
        autoload -U -- "${function_name}"
    }
}

# ....................{ STARTUP                            }....................
# Autoload zsh module-defined functions at the beginning rather than end of
# zeshy startup, thus avoiding errors when subsequent zeshy startup functions
# call such functions.
run_hook_on_zeshy_startup_first startup_zeshy_autoloads

declare_function_with_stdin <<'/---'
void startup_zeshy_autoloads(void)

Autoload all canonical functions and globals bundled by stock `zsh`
installations, including:

* All autoloadable functions in directories listed by the default ${fpath}.
* All autoloadable functions and globals defined by precompiled `zsh` modules,
  thus deferring loading each such module until the first call of such function
  or expansion of such variable.
/---
function startup_zeshy_autoloads() {
    # Validate sanity.
    die_if_args

    # Do *NOT* attempt to autoload extraneous dotfiles as zsh functions (e.g.,
    # ".keep_app-shells_zsh-0" under Gentoo). While we could also effect this
    # with an exclusion in the glob expression below (e.g.,
    # "${^fpath}/*~*/.[^/]#(.:t)"), zsh options are somewhat more efficient.
    enable_shell_option_locally no_glob_dots

    # Define autoloads for all autoloadable functions in the current user's
    # function path list, obsoleting manual autoloading of such functions.
    # Since the set of all basenames of all plain files in all directories in
    # such list is exactly the set of all autoloadable function names, globbing
    # such basenames suffices to find all such names. Dismantled, this is:
    #
    # * "${^fpath}", iteratively expanding to each directory containing
    #   autoloadable function files.
    # * "/*", globbing all files and subdirectories of such directory.
    # * ".", excluding non-plain files (e.g., device files, directories).
    # * ":t", expanding to the tail (i.e., basename) of each such file.
    #
    # Ideally, zeshy would perform such autoloading at recompilation time.
    # Unfortunately, zsh cannot compile autoload stubs into digest files.
#   print 'ok'
#   print 'ko'
    string function_name
    for    function_name (${^fpath}/*(.:t)) {
        # For safety, autoload this function under option "-U" to suppress alias
        # expansion. This prevents expansion of Zeshy- and user-specific aliases
        # under core zsh functions implemented with no such aliases in mind.
        # Avoid calling autoload_function(), which is (unsurprisingly) slow
        # under iteration.
#       print "autoloading \"${function_name}\"..."
        autoload -U -- "${function_name}"
    }

    # Define autoloads for all autoloadable functions defined by zsh modules not
    # autoloaded above. Unlike above, this requires manually mapping modules to
    # function names -- an unctuous proposition. Dismantled, this is:
    #
    # * "-a", autoloading the following builtins ("b"), functions ("f"), or
    #   parameters ("p") from the immediately following module.
    # * "i", ignoring rather than failing when such builtins, functions, or
    #   parameters have already been loaded within such module.
    #
    # Avoid mapping modules already loaded by the main Zeshy script as well as:
    #
    # * "mapfile", whose overly permissive style poses a profound risk of
    #   permanent, silent file deletion or modification. For safety, this module
    #   must be manually loaded and unloaded.
    # * "prof", which when loaded begins aggressively profiling zsh functions.
    #   While useful, autoloading the zprof() function this module defines will
    #   begin profiling *ONLY* at the first call of such function, whereas such
    #   function is intended to be called after having already loaded this
    #   module and performed the profiled code. By intentional design, this
    #   module must be manually loaded and unloaded.
    zmodload -abi zsh/cap cap getcap setcap
    zmodload -abi zsh/clone clone
    zmodload -abi zsh/compctl compctl compcall
    zmodload -abi zsh/computil comparguments compdescribe compfiles compgroups\
        compquote comptags comptry compvalues
    zmodload -afi zsh/deltochar delete-to-char zap-to-char
    zmodload -afi zsh/mathfunc abs int float acos acosh asin asinh atan atanh\
        cbrt ceil cos cosh erf erfc exp expm1 fabs floor gamma j0 j1 lgamma log\
        log10 log1p logb sin sinh sqrt tan tanh y0 y1 ilogb signgam copysign\
        fmod hypot nextafter jn yn ldexp scalb rand48
    zmodload -abi zsh/pcre pcre_compile pcre_study pcre_match
    zmodload -abi zsh/sched sched
    zmodload -abi zsh/net/socket zsocket
    zmodload -abi zsh/net/tcp ztcp
    zmodload -abi zsh/system syserror sysread syswrite
    zmodload -api zsh/system errnos
    zmodload -abi zsh/termcap echotc
    zmodload -api zsh/termcap termcap
    zmodload -abi zsh/zftp zftp
    zmodload -api zsh/zleparameter keymaps widgets
    zmodload -abi zsh/zpty zpty
    zmodload -abi zsh/zselect zselect
}

# --------------------( WASTELANDS                         )--------------------
#FUXME: run_hook_on_zeshy_startup_first() -- or, rather, the way "src/compile"
#is currently calling such function -- requires a look. Ideally, all startup
#hooks should be called *AFTER* defining all initial values for globals and
#aliases, thus providing such hooks access to such values. We appear, however,
#to 

    #FUXME: Bizarre. I feel like I've confronted this issue before, but...
    #here we go again. This function is unaware of aliases defined by a prior
    #function. Presumably, so are *ALL* other startup functions. The solution,
    #I strongly suspect (and am unclear as to why I haven't already implemented)
    #is to:
    #
    #* Extract *JUST* the call to startup_zeshy_aliases() (and *NOT the
    #  similar call to startup_zeshy_globals(), which appears to work) from
    #  startup_zeshy().
    #* Explicitly call startup_zeshy_aliases() immediately before calling
    #  startup_zeshy() in "src/main".
    #
    #This is a bit crazy, but not *TOO* far off the map. Document voluminously,
    #of course.
#   print "enable_shell_option_locally:"
#   which enable_shell_option_locally
#run_hook_on_zeshy_precompile precompile_zeshy_autoloads

#function precompile_zeshy_autoloads() {
    # Validate sanity.
#   die_if_args

    # Do *NOT* attempt to autoload extraneous dotfiles as zsh functions (e.g.,
    # ".keep_app-shells_zsh-0" under Gentoo). While we could also effect this
    # with an exclusion in the glob expression below (e.g.,
    # "${^fpath}/*~*/.[^/]#(.:t)"), zsh options are somewhat more efficient.
#   enable_shell_option_locally no_glob_dots

    # Define autoloads for all autoloadable functions in the current user's
    # function path list, obsoleting manual autoloading of such functions.
    # Since the set of all basenames of all plain files in all directories in
    # such list is exactly the set of all autoloadable function names, globbing
    # such basenames suffices to find all such names. Dismantled, this is:
    #
    # * "${^fpath}", iteratively expanding to each directory containing
    #   autoloadable function files.
    # * "/*", globbing all files and subdirectories of such directory.
    # * ".", excluding non-plain files (e.g., device files, directories).
    # * ":t", expanding to the tail (i.e., basename) of each such file.
#   for function_name (${^fpath}/*(.:t)) {
        # For safety, autoload this function under option "-U" to suppress alias
        # expansion. This prevents expansion of Zeshy- and user-specific aliases
        # under core zsh functions implemented with no such aliases in mind.
        # Avoid calling autoload_function(), which is (unsurprisingly) slow
        # under iteration.
#       print "autoloading \"${function_name}\"..."
#       autoload -U -- "${function_name}"
#   }
#}

#   for function_name (${^fpath}/*~*/.[^/]#(.:t)) {
#FUXME: Change to run_hook_on_zeshy_precompile_first() after creating such a
#function.
#run_hook_on_zeshy_precompile precompile_zeshy_autoloads

# "os/kernel"
#     Handling *nix kernel modules.
