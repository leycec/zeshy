#!/usr/bin/env zsh
# ====================[ file                               ]====================
#                     [ Time-stamp: "2009-04-18 19:29:36 leycec" ]
#
# --------------------( SYNOPSIS                           )--------------------
# Zeshy File. This Zeshy component implements supplementary file- and path-
# handling functionality, over and above that already implemented by Zeshy Core.

# ....................{ ALIASES                            }....................
# Three letter.
alias dus="print_disk_usage_sorted"
alias mvt="mv_to_temp_dir"

# Filesystem checking (fsck). Note we do not implicitly pass the "-a" or "-p"
# options to fsck, as fsck ignores some filesystem errors (e.g., superblock
# mount time in the future) when those options are passed.
export ZESHY_FSCK_OPTIONS="-C -M -T"
alias fsck="fsck ${ZESHY_FSCK_OPTIONS}"
alias fsck.ext2="fsck.ext2 ${ZESHY_FSCK_OPTIONS}"
alias fsck.ext3="fsck.ext3 ${ZESHY_FSCK_OPTIONS}"
alias fsck.vfat="fsck.vfat ${ZESHY_FSCK_OPTIONS}"

# ....................{ ALIASES ~ guis                     }....................
# Disk usage.
baobab()    { command baobab    "${@}" & }
filelight() { command filelight "${@}" & }

# ....................{ EXCEPTIONS                         }....................
# void die_unless_installed(char *command_name)
#
# Raise an exception unless the passed command exists.
die_unless_installed() {
    die_unless_one_argument 'expected exactly one command name'
    local dirname="${1}"
    is_installed "${dirname}" || die "\"${dirname}\" not found"
}

# void die_unless_dir(char *dirname)
#
# Raise an exception unless the passed directory exists.
die_unless_dir() {
    die_unless_one_argument 'expected exactly one dirname'
    local dirname="${1}"
    is_dir "${dirname}" || die "\"${dirname}\" not found"
}

# ....................{ FILENAMES                          }....................
# bool is_path_absolute(char *path)
#
# Return success if the passed path is absolute (i.e., begins with a '/'
# character). Otherwise, failure.
is_path_absolute() {
    die_unless_one_argument 'expected exactly one path'
    [[ "${1[1]}" == / ]]
}

# char *get_basename(char *path)
#
# Get the basename of the passed path (i.e., the path sans any parent direname).
# This function is a thin wrapper around the "basename" binary for convenience.
get_basename() {
    die_unless_one_argument 'expected exactly one path'
    try basename "${1}"
}

# char *get_filename_sans_filetype(char *filename)
#
# Get the filename of the passed filename sans filetype. If this filename has
# no filetype, this is the filename itself. By design, this only excludes the
# rightmost filetype: e.g.,
#
#     >>> get_basename_sans_filetype('/endymion/hastur.tar.gz')
#     /endymion/hastur.tar
get_filename_sans_filetype() {
    die_unless_one_argument 'expected exactly one filename'
    echo "${1%.*}"
}

# char *get_filetype(char *filename)
#
# Get the filetype of the passed filename. If this filename has no filetype,
# this is the empty string. Note the only matches the rightmost filetype: e.g.,
#
#     >>> get_filetype('hastur.tar.gz')
#     gz
get_filetype() {
    die_unless_one_argument 'expected exactly one filename'
    local filename="${1}"
    local filetype="${filename##*.}"

    # If this filename has no filetype, then the above parameter expansion
    # matched the filename itself, in which case we test for and ignore this. 
    [[ "${filetype}" == "${filename}" ]] || echo "${filetype}"
}

# ....................{ FILETYPES                          }....................
# bool is_file_binary(char *filename)
#
# Return success if the passed filename corresponds to a binary file (i.e., not
# a text file). (If the passed filename corresponds to a directory instead, this
# function always returns failure.)
is_file_binary() {
    die_unless_one_argument 'expected exactly one filename'
    local filename="${1}"

    is_file "${filename}" || return "${ZESHY_FAILURE_CODE}"
    file --mime-encoding "${filename}" | grep --quiet ': binary$'
}

# bool is_file_text(char *filename)
#
# Return success if the passed filename corresponds to a text file (i.e., not a
# binary file). (If the passed filename corresponds to a directory instead, this
# function always returns failure.)
is_file_text() {
    die_unless_one_argument 'expected exactly one filename'
    local filename="${1}"

    # Do not simply do "! is_file_binary", as that incorrectly returns success
    # on non-files.
    is_file "${filename}" || return "${ZESHY_FAILURE_CODE}"
    file --mime-encoding "${filename}" | grep --invert-match --quiet ': binary$'
}

# ....................{ FILESIZES                          }....................
# Aliases.
alias df="df --human-readable --sync"
alias du="du --human-readable --total"
alias dup="print_disk_usage_paged"
alias dus="print_disk_usage_sorted"

print_disk_usage_paged() {
    zpage du "${@}"
}

print_disk_usage_paged_sorted() {
    du "${@}" | sort --human-numeric-sort --reverse | "${PAGER}"
#  [ -z "$1" ] && 1="."
#  \du --all --no-dereference $1 | sort --numeric-sort --reverse | less
}

# ....................{ MOUNTS                             }....................
# bool is_dir_mounted(char *dirname)
#
# Return success if the passed directory name is an existing mount point (i.e.,
# if "something" is currently mounted under this directory).
is_dir_mounted() {
    die_unless_one_argument 'expected exactly one dirname'
    local mount_point="${1}"
    
    # If the passed pathname does not correspond to an existing directory, it
    # cannot correspond to a valid mount point.
    is_dir "${mount_point}" || return "${ZESHY_FAILURE_CODE}"

    # If the "mountpoint" binary is available, use that.
    if is_installed mountpoint
    then mountpoint -q "${mount_point}"
    # Otherwise, manually grep "${ZESHY_MOUNT_POINTS_FILE}".
    else grep -q "${mount_point}" "${ZESHY_MOUNT_POINTS_FILE}"
    fi
}

# ....................{ FINDERS                            }....................
# char *which_files_nonfatal(char *filename1, *filename2, ...)
#
# Get the absolute path to the first filename in the list of passed filenames
# which exists: e.g.,
#
#     >>> which_files_nonfatal '/the_ancient.one' '/the_king_in.yellow' '/tmp'
#     /tmp
which_files_nonfatal() {
    die_unless_at_least_one_argument 'expected exactly one filename'
    
    # While there are more filenames to consider, shift the current filename to
    # be considered to a local variable "path".
    local filename
    while is "${1}"; do
        filename="${1}"
        shift

        # If this filename exists, return it.
        if is_file "${filename}"; then
            print  "${filename}"
            return ${ZESHY_SUCCESS_CODE}
        fi
    done
    
    # No filenames in the list of passed filenames exist. Return with failure.
    false
}

#FIXME: Should this be "which_commands_nonfatal", rather?
# char *which_binaries_nonfatal(char *binary_name1, *binary_name2, ...)
#
# Get the absolute path to the first binary in the list of passed binaries
# which exists: e.g.,
#
#     >>> which_binaries_nonfatal Council_of_Nicea Constantinople zsh
#     /bin/zsh
# 
# This function is intended to automate selection of binaries, in descending
# order of preference. (In the above example, the hypothetical
# "Council_of_Nicea" binary would be selected before all following binaries on
# systems having the binary.)
which_binaries_nonfatal() {
    die_unless_at_least_one_argument 'expected exactly one binary name'
    
    # While there are more binaries to consider, shift the current binary to be
    # considered to a local variable "binary_name".
    local binary_name binary_path
    while is "${1}"; do
        binary_name="${1}"
        shift

        # If this binary exists, return the absolute path to it.
        binary_path="$(which_binary_nonfatal "${binary_name}")"
        if is_ok; then
            print "${binary_path}"
            return ${ZESHY_SUCCESS_CODE}
        fi
    done
    
    # No binaries in the list of passed binaries exist. Return with failure.
    false
}

# ....................{ TEMPORARIES                        }....................
#FIXME: This is probably OS-specific, but perhaps good enough for now.
# char *which_temp_dir(void)
#
# Get the absolute path to the temporary directory. This is, in descending order
# of preference:
#
# * "${HOME}/tmp", if the current user's home directory has such a subdirectory. 
# * "/tmp", otherwise.
which_temp_home() {
    die_unless_no_arguments 'expected no arguments'
    
    local user_temporary_dir="${HOME}/tmp" root_temporary_dir="/tmp"
    if   is_dir "${user_temporary_dir}"
    then print  "${user_temporary_dir}"
    elif is_dir "${root_temporary_dir}"
    then print  "${root_temporary_dir}"
    else
        die "neither \"${user_temporary_dir}\" or \"${root_temporary_dir}\""\
            "found; no temporary directory found"
    fi
}

# char *which_temp_dir(char *prefix)
#
# Get the absolute path to a temporary path having the passed prefix: e.g.,
#
#     >>> which_temp_dir git_patches
#     /tmp/git_patches~2011-04-18_22-58-34  # assuming today is 2011-04-18
#
# While subtle race conditions may arise with concurrent processing, this path
# is generally unique. This should probably not be depended upon for mission
# critical ZSH scripts, however. (Of course, if you're coding such a mission
# critical script in ZSH, you've probably already gone too far.)
which_temp_dir() {
    die_unless_one_argument 'expected exactly one dirname prefix'
    
    local which_temp_dir
    while true; do
        which_temp_dir="$(which_temp_home)/${1}.path~$(date +'%F_%H-%M-%S')"
        if ! is_dir "${which_temp_dir}"; then
            print  "${which_temp_dir}"
            break
        fi
    done
}

# char *which_temp_file(char *prefix)
#
# Get the absolute path to a temporary file having the passed prefix: e.g.,
#
#   # Prints "/tmp/git_patch_file~2011-12-01_18-37-41", for example.
#   which_temp_file git_patch_file
#
# Subtle race conditions may arise, as above.
which_temp_file() {
    die_unless_one_argument 'expected exactly one filename prefix'
    
    local which_temp_file
    while true; do
        which_temp_file="$(which_temp_home)/${1}.file~"$(date +'%F_%H-%M-%S')
        if ! is_file "${which_temp_file}"; then
            print  "${which_temp_file}"
            break
        fi
    done
}

# ....................{ TEMPORARIES ~ mutators             }....................
# void mv_to_temp_dir(char *path1, char *path2, ...)
#
# Move the passed set of files and/or directories to the "/tmp" directory. For
# Additionally, this suffixing all files and
# paths that already exist in "/tmp/" with the current time. 
mv_to_temp_dir() {
    local target_path source_path source_basename

    # Quote the list of all passed paths with "(q)" to avoid improperly
    # splitting individual paths on whitespace.
    for source_path in "${(q)@}"; do
        source_basename=$(get_basename "${source_path}")
        target_path="/tmp/${source_basename}"
        is_path "${target_path}" &&
            target_path="$(which_temp_home)/$source_basename~$(date +'%F_%H-%M-%S')"
        
        try mv "${source_path}" "${target_path}"
    done
}

# --------------------( COPYRIGHT AND LICENSE              )--------------------
# The information below applies to everything in this distribution,
# except where noted.
#              
# Copyright 2007-2011 by Cecil Curry.
#   
#   http://www.raiazome.com
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.
