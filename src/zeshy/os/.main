#!/usr/bin/env zsh
# ====================[ os                                 ]====================
#                     [ Time-stamp: "2009-04-18 19:29:36 leycec" ]
#
# --------------------( SYNOPSIS                           )--------------------
# Zeshy OS. This Zeshy component implements operating system functionality
# generic to all operation systems.

# ....................{ DEPENDENCIES                       }....................
zimport zeshy/file
zimport zeshy/string

# void _zeshy_os(void)
#
# Import Zeshy components specific to the current OS.
_zeshy_os() {
    local current_os_dir="$(dirname "${zeshy_script_path}")/$(get_os_name)"
    is_dir "${current_os_dir}" and zimport "${current_os_dir}"
}

# ....................{ OS                                 }....................
# void get_os_name(void)
#
# Get the (strictly lowercase) name of the distro running on the current system:
# e.g.,
#
#     >>> get_os_name
#     freebsd   # under FreeBSD systems
#     gnu-linux # under GNU/Linux systems
get_os_name() {
    # "uname" reports Linux as "GNU/Linux", so replace backslashes with dashes.
    string os_name="$(to_lowercase "$(uname --operating-system)")"
    os_name="${os_name//\//-}"
    echo "${os_name}"
}

# ....................{ OS ~ undefined                     }....................
# The following functions are OS-specific, and thus defined for each OS-specific
# Zeshy component, elsewhere. By default, each dies with error.

# char *get_current_process_basename(void)
#
# Get the basename of the binary corresponding to the current process.
get_current_process_basename() { die "unimplemented on $(get_os_name)" }

# char *get_current_process_command_line(void)
#
# Get the command line under which the current process is executing. This is a
# single string having these parts:
#
# * Absolute OR relative path to the executable file from which the process was
#   executed. Note that, in the latter case, there is no reliable mechanism for
#   converting the relative path into an absolute path.
#
# Due to the above unreliabilities, this string is best used for displaying non-
# critical log and user interface labels rather than critical functionality.
get_current_process_command_line() { die "unimplemented on $(get_os_name)" }
 
# ....................{ DISK USAGE ~ cleaning              }....................
# void clean_machine(void)
#
# Safely clean this machine of all transient files and directories.
clean_machine() {
    #FIXME: Prefix this with an interactive query: "Are you SURE you want...?"
    #Also, it would be nice to implement a clever "distfile" cleaner that ONLY
    #removed distfiles for which there is no corresponding installed version.
    if is_installed ccache; then
        utter "cleaning \"ccache\"..."
        try_as_superuser ccache -C
    fi
    
    local cleanable_dir
    for   cleanable_dir in "/var/tmp/paludis/build/"; do
        if [[ -d "${cleanable_dir}" ]]; then
            utter "cleaning \"${cleanable_dir}\"..."
            try_as_superuser rm -rf "${cleanable_dir}"/*
        fi
    done
}

# ....................{ MEMORY USAGE                       }....................
# int get_memory_total(void)
#
# Get the amount of total available memory on this machine in bytes. As
# expected, the following invariant holds:
#
#   >>> [[ $(get_memory_total) -eq
#   ...    (( $(get_memory_free) + $(get_memory_used) )) ]] && echo 'Equal!"
#   Equal!
get_memory_total() {
    # Return the second word of the second line.
    [[ "${#}" -eq 0 ]] || die 'expected no arguments'
    echo ${"${"$(free -bt)"[(f)2]}"[(w)2]}
    #FIXME: Alternate implementations; keep around!
    #echo $(match_multiline '^Mem:\s+(\d+)' "$(free -bt)")
    #$(cat '/proc/meminfo' | awk -F '[ ]+' '/^MemTotal/ { print $2 }')
}

# int get_memory_free(void)
#
# Get the amount of free available memory on this machine in bytes.
get_memory_free() {
    [[ "${#}" -eq 0 ]] || die 'expected no arguments'
    echo ${"${"$(free -bt)"[(f)2]}"[(w)4]}
}

# int get_memory_used(void)
#
# Get the amount of used available memory on this machine in bytes.
get_memory_used() {
    [[ "${#}" -eq 0 ]] || die 'expected no arguments'
    echo ${"${"$(free -bt)"[(f)2]}"[(w)3]}
}

# void print_memory_use_overview(void)
#
# Print an overview of current memory use, including free and used RAM in MB. 
print_memory_use_overview() {
    [[ "${#}" -eq 0 ]] || die 'expected no arguments'
    utter "showing memory use in megabytes..."
    try free -mt
}

# ....................{ MAIN                               }....................
# Call and then immediately unset (i.e., delete) _zeshy_os().
_zeshy_os
unset _zeshy_os

# --------------------( COPYRIGHT AND LICENSE              )--------------------
# The information below applies to everything in this distribution,
# except where noted.
#              
# Copyright 2007-2011 by Cecil Curry.
#   
#   http://www.raiazome.com
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.
