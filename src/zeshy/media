#!/usr/bin/env zsh
# ====================[ media                              ]====================
#                     [ Time-stamp: "2009-04-18 19:29:36 leycec" ]
#
# --------------------( SYNOPSIS                           )--------------------
# Zeshy Media, implementing media-specific functionality.

# ....................{ DEPENDENCIES                       }....................
zimport zeshy/file

# ....................{ PATHS                              }....................
# Directory to which Zeshy mounts media.
export ZESHY_MOUNT_HOME='/media'

# Directory to which Zeshy mounts ISOs.
export ZESHY_MOUNT_ISO_PATH="${ZESHY_MOUNT_HOME}/iso"

#FIXME: Generalize to non-Linux *nix platforms.
# File to which Zeshy inspects existing mount points.
export ZESHY_MOUNT_POINTS_FILE="/proc/mounts"

# ....................{ ALIASES                            }....................
# Mounting.
alias mo="mount"
alias umo="umount"

# Images.
dia()    { command dia "${@}" & } 
geeqie() { command geeqie "${@}" & } 
gimp()   { command gimp "${@}" & } 

# Images. (Inkscape.)
inkscape() { command inkscape "${@}" & } 
inkscape_export_eps() {
    inkscape $* --export-eps="${1}.eps"
}
inkscape_export_pdf() {
    inkscape --export-area-drawing $* --export-pdf="${1}.pdf"
}

# Videos.
vlc() { command vlc "${@}" & }

# ....................{ MOUNTS                             }....................
# bool is_dir_mounted(char *dirname)
#
# Return success if the passed directory name is an existing mount point (i.e.,
# if "something" is currently mounted under this directory).
is_dir_mounted() {
    die_unless_one_argument 'expected exactly one dirname'
    string mount_point="${1}"
    
    # If the passed pathname does not correspond to an existing directory, it
    # cannot correspond to a valid mount point.
    is_dir "${mount_point}" or return_failure

    # If the "mountpoint" binary is available, use that.
    if is_installed mountpoint
    then mountpoint -q "${mount_point}"
    # Otherwise, manually grep "${ZESHY_MOUNT_POINTS_FILE}".
    else grep -q "${mount_point}" "${ZESHY_MOUNT_POINTS_FILE}"
    fi
}

# void mount_iso(char *filename)
#
# Mount the passed ISO filename to the "${ZESHY_MOUNT_ISO_PATH}" directory. If
# something is already mounted to this path, this function iteratively appends
# numbers (starting at 1), until discovering a directory name to which nothing
# is currently mounted.
mount_iso() {
    # Ensure sanity.
    die_unless_one_argument 'expected exactly one ISO filename'
    string iso_filename="${1}"
    [[ "${iso_filename}" == *.iso ]] or
        die "\"${iso_filename}\" not an ISO filename"
    die_unless_file "${iso_filename}"
    string ZESHY_SCRIPT_NAME='mount_iso'

    # Find a suitable mount path.
    integer iso_mount_path_suffix=0
    string  iso_mount_path_prefix="${ZESHY_MOUNT_ISO_PATH}"\
            iso_mount_path="${iso_mount_path_prefix}"
    utter 'searching for available mount point...'
    while is_dir_mounted "${iso_mount_path}"; do
        iso_mount_path_suffix=$(( iso_mount_path_suffix + 1 ))
        iso_mount_path="${iso_mount_path_prefix}${iso_mount_path_suffix}"
    done
    
    # Mount the passed ISO filename to the desired mount path.
    #
    # Note that this does not pass the ",user" option when mounting, as doing so
    # would implicitly enables unhelpful mount options (e.g., "noexec").
    utter "mounting \"${iso_filename}\" to \"${iso_mount_path}\"..."
    run_as_superuser make_directory_if_not_found "${iso_mount_path}" 
    run_as_superuser mount -t iso9660 -o 'exec,loop'\
        "${iso_filename}" "${iso_mount_path}" or
        die "\"${iso_filename}\" not mountable to \"${iso_mount_path}\""
}

# --------------------( COPYRIGHT AND LICENSE              )--------------------
# The information below applies to everything in this distribution,
# except where noted.
#              
# Copyright 2007-2012 by Cecil Curry.
#   
#   http://www.raiazome.com
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.
