#!/usr/bin/env zeshy
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2017 by Cecil Curry.
# See "LICENSE" for additional details.
#
# --------------------( SYNOPSIS                           )--------------------
# (Re)compile the `zeshy` codebase into the current user's `zeshy` digest file.

# ....................{ DEPENDENCIES ~ zsh                 }....................
# Load all zsh modules required by this parcel or parcels this parcel sources.
# Since this only applies to digest file compilation, we still retain autoload
# declarations for all such modules in @{zsh/module}.

# Bind "libpcre" functions to zsh builtins, thus exposing Perl-compatible
# Regular Expression (PCRE) functionality during digest file compilation. In
# particular, alias and function declarations require such functionality for
# matching the admittedly obtuse prototype syntax prefixing help strings.
zmodload zsh/pcre

# ....................{ DEPENDENCIES ~ zeshy               }....................
# Source all zeshy parcels *NOT* intended to be compiled into the digest file.
# For simplicity, this is exactly the set of all zeshy scripts in subdirectories
# of the current directory sourced in lexicographic order.
#
# Parcels intended to be compiled into the digest file (e.g., @{finalize}) are
# sourced below after cataloguing the set of all currently defined operables.
::script_zsh.source "${ZY_SCRIPT_DIRNAME}"/*/**/*.zy

# ....................{ GLOBALS                            }....................
# Lists of all zeshy-specific aliases, functions, and globals. Defining such
# lists globally:
#
# * Implicitly exports such lists to child shells, which on receiving such
#   globals may use their contents to undefine aliases, functions, and other
#   globals imported from the parent shell.
# * Permits us to undefine such globals in a global context below. See the
#   tail of this file.
#
# zsh treats suffix aliases differently from normal and global aliases, so
# differentiate the former from the latter.
typeset -gaU\
    ZY__ALIAS_NAMES\
    ZY__ALIAS_SUFFIX_NAMES\
    ZY__FUNCTION_NAMES\
    ZY__GLOBAL_NAMES\
    ZY__HOOKS_RUN_ON_ZY_PRECOMPILE

typeset -g\
    ZY__SCRIPT_FILENAME_GLOB_EXCLUDE\
    ZY__SCRIPT_FILENAME_GLOB

#FIXME: After renaming *ALL* zeshy scripts to have filetype ".zy", excluding
#non-zeshy scripts will become both considerably simpler and more reliable.
#Indeed, one pleasant side effect of doing so is that we may then litter the
#codebase with plaintext "README" files intended for developer eyes only.
#FIXME: Document me.

# Avoid sourcing the following types of files as zeshy scripts:
#
# * "vim"-specific cache files.
ZY__SCRIPT_FILENAME_GLOB_EXCLUDE='~*.swp'
ZY__SCRIPT_FILENAME_GLOB='*'${ZY__SCRIPT_FILENAME_GLOB_EXCLUDE}'(-.)'

# ....................{ RUNNERS                           }....................
#FIXME: Excise such nonsensicality.
#FIXME: Hmm; why, again? As I dimly recall, we were going to eliminate the
#concept of "precompilation" entirely. Is that correct? If so, what did we
#intend to replace that concept with -- if anything?
#FIXME: Ah! Right. Thanks to @{zz-stop}, we no longer need such hooks. Yay!

# void :run_hook_on_zeshy_precompile(string code)
function :run_hook_on_zeshy_precompile() {
    (( # == 1 )) || :die 'Expected one code string.'
    ZY__HOOKS_RUN_ON_ZY_PRECOMPILE+="${1}"
#   print "registering precompilation hook \"${1}\""
}

# ....................{ COMPILERS                         }....................
# :void ::zeshy.compile()
function ::zeshy.compile() {
    (( # == 0 )) || :die 'Expected no arguments.'

    # List of all currently declared aliases, functions, and globals declared
    # *BEFORE* sourcing and compiling zeshy. See ::zeshy.compile_start().
    local -a\
        ZY__ALIAS_NAMES_OLD\
        ZY__ALIAS_SUFFIX_NAMES_OLD\
        ZY__FUNCTION_NAMES_OLD\
        ZY__GLOBAL_NAMES_OLD

    # List of all currently declared aliases, functions, and globals declared
    # *AFTER* sourcing and compiling zeshy. See ::zeshy.precompile_names().
    local -a\
        ZY__ALIAS_NAMES_NEW\
        ZY__ALIAS_SUFFIX_NAMES_NEW\
        ZY__FUNCTION_NAMES_NEW\
        ZY__GLOBAL_NAMES_NEW

    # Prepare to source the zeshy codebase.
    ::zeshy.compile_start

    # !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    # Do *NOT* expand zeshy aliases sourced by this function in any
    # subsequently called functions defined by this parcel. Since zsh expands
    # aliases at function definition rather than call time, such aliases will
    # *NOT* be expanded and hence induce "command not found" errors at call
    # time. The devil is in the obscura.
    # !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    #
    # Source all zeshy scripts applicable to the current system and user.
    ::zeshy.compile_source

    # Source the @{finalize} script *AFTER* sourcing all other scripts. While
    # the former could technically be sourced *BEFORE* the latter, the current
    # approach permits such script to leverage operables defined by the zeshy
    # codebase -- thus simplifying otherwise contumely logic.
    ::script_zsh.source finalize

    # Finalize which operables are to be compiled into the digest file.
    ::zeshy.compile_finalize

    # Compile all zeshy-specific functions (and hence aliases and globals) into
    # zeshy's user digest file *AFTER* the above finalization.
    ::zeshy.compile_digest
}

# :void ::zeshy.compile_start()
function ::zeshy.compile_start() {
    (( # == 0 )) || :die 'Expected no arguments.'

    #FIXME: Technically, both alias and function names may contain newlines. If
    #even a single such alias or function exists, the line-splitting performed
    #below could fail spectacularly. It's unclear how to guard against this.
    #Perhaps calling "alias -m *$'\n'*" or some such and testing whether at
    #least one such alias or function exists before calling "alias +"?
    #FIXME: Forehead smacking time. Given canonical list global ${functions},
    #we can efficiently obtain the set of all newly defined functions by
    #capturing such list before sourcing the current script into a temporary
    #list, then differencing the current contents of ${functions} with such
    #temporary list after such sourcing. Super efficient! Indeed, such approach
    #can and should be used elsewhere in ={compile} to perform efficient names
    #differencing, and can and should be extended to both aliases and globals.

    # Record the names of all currently defined aliases, functions, and globals
    # *NOT* to be compiled into zeshy's user digest file. See
    # :set_list_to_aliases(), :set_list_to_functions(), and
    # :set_list_to_globals() for further details.
    ZY__ALIAS_NAMES_OLD=(        "${(@f)"$(alias +)"}" )
    ZY__ALIAS_SUFFIX_NAMES_OLD=( "${(@f)"$(alias +s)"}" )
    ZY__FUNCTION_NAMES_OLD=(     "${(@f)"$(typeset +f)"}" )
    ZY__GLOBAL_NAMES_OLD=(   ${${"${(@f)"$(typeset +g +)"}":#* local *}##* } )

    # Filter all non-zeshy-specific globals from such list. See
    # ::zeshy.precompile_names() for further details.
    ZY__GLOBAL_NAMES_OLD=( "${(M)ZY__GLOBAL_NAMES_OLD[@]:#ZY_*}" )
#   print -l "===ZY__GLOBAL_NAMES_OLD===" "${ZY__GLOBAL_NAMES_OLD[@]}"
#   { print 'old globals:'; typeset -g + } | less
}

# :void ::zeshy.precompile_names()
function ::zeshy.precompile_names() {
    (( # == 0 )) || :die 'Expected no arguments.'

    # Record the names of all currently defined aliases, functions, and
    # globals. (See ::zeshy.compile_start() for details.)
    ZY__ALIAS_NAMES_NEW=(        "${(@f)"$(alias +)"}" )
    ZY__ALIAS_SUFFIX_NAMES_NEW=( "${(@f)"$(alias +s)"}" )
    ZY__FUNCTION_NAMES_NEW=(     "${(@f)"$(typeset +f)"}" )
    ZY__GLOBAL_NAMES_NEW=(   ${${"${(@f)"$(typeset +g +)"}":#* local *}##* } )

    # Filter all non-zeshy-specific globals from such list (i.e., globals *NOT*
    # prefixed by "ZY_"). While such negative filtering is somewhat
    # overzealous, positively filtering the set of all non-zeshy-specific
    # globals remains both tedious and fragile. We would need to match the set
    # of all:
    #
    # * Locals defined by functions calling this function, which are
    #   effectively indistinguishable from globals.
    # * Core zsh globals (e.g., ${MATCH}, ${match}).
    # * Module-specific zsh globals (e.g., ${errnos}, ${sysparams}).
    #
    # Note that such filtering dramatically reduces the size of such set but
    # does *NOT* eliminate the need to difference such set against the set of
    # all globals defined prior to sourcing the zeshy codebase. Why?  The
    # current environment could conceivably define globals prefixed by "ZY_"
    # *NOT* defined by the zeshy codebase, in which case such globals must be
    # ignored rather than compiled into the current digest file.
    #
    # See :List.items.remove_not_^=() for further details. (Since there exists
    # no guarantee such function was successfully declared by this time, avoid
    # calling such function.)
    ZY__GLOBAL_NAMES_NEW=( "${(M)ZY__GLOBAL_NAMES_NEW[@]:#ZY_*}" )

    # The lists of zeshy-specific aliases, functions, and globals are the
    # asymmetric differences of the old from new lists of zsh aliases,
    # functions, and globals. See set_list_to_list_difference() for further
    # details. (Again, avoid calling such function.)
    ZY__ALIAS_NAMES=(
        "${ZY__ALIAS_NAMES_NEW[@]:|ZY__ALIAS_NAMES_OLD}" )
    ZY__ALIAS_SUFFIX_NAMES=(
        "${ZY__ALIAS_SUFFIX_NAMES_NEW[@]:|ZY__ALIAS_SUFFIX_NAMES_OLD}" )
    ZY__FUNCTION_NAMES=(
        "${ZY__FUNCTION_NAMES_NEW[@]:|ZY__FUNCTION_NAMES_OLD}" )
    ZY__GLOBAL_NAMES=(
        "${ZY__GLOBAL_NAMES_NEW[@]:|ZY__GLOBAL_NAMES_OLD}" )
#   print -lr "===ZY__GLOBAL_NAMES_OLD===" "${ZY__GLOBAL_NAMES_OLD[@]}"
#   print -lr "===ZY__GLOBAL_NAMES_NEW===" "${ZY__GLOBAL_NAMES_NEW[@]}"
  # print -lr "===ZY__GLOBAL_NAMES==="     "${ZY__GLOBAL_NAMES[@]}"
#   print -lr "===ZY__FUNCTION_NAMES===" "${ZY__FUNCTION_NAMES[@]}"
#   print -lr "===ZY_STARTUP_RUNNABLES===" "${ZY_STARTUP_RUNNABLES[@]}"
#   print -rl -- "globals to be compiled:" "${ZY__GLOBAL_NAMES[@]}" | less
#   print "aliases: ${aliases}\nsuffix_aliases: ${suffix_aliases}"

    # If no zeshy-specific aliases, functions, or globals were defined, throw
    # an exception. Logic below and elsewhere implicitly assumes at least one
    # such alias, function, and global to exist. Indeed, if this is *NOT* the
    # case, something has gone *HORRIBLY* awry.
    #
    # Since the definition of zeshy-specific suffix aliases depends on the
    # existence of optional external files (e.g., "/etc/mailcap"), the non-
    # existence of such aliases does *NOT* constitute a fatal error. Moreover,
    # since no core zeshy functionality depends on such aliases, their non-
    # existence does *NOT* constitute grounds for even non-fatal warnings.
    #
    # Since die_unless_list_nonempty() may not have even been defined, test
    # such lists manually rather than calling such function.
    (( ${#ZY__ALIAS_NAMES} )) || :die\
        'No zeshy aliases defined (i.e., ${ZY__ALIAS_NAMES} empty).'
    (( ${#ZY__FUNCTION_NAMES} )) || :die\
        'No zeshy functions defined (i.e., ${ZY__FUNCTION_NAMES} empty).'
    (( ${#ZY__GLOBAL_NAMES} )) || :die\
        'No zeshy globals defined (i.e., ${ZY__GLOBAL_NAMES} empty).'
}

# :void ::zeshy.compile_source()
function ::zeshy.compile_source() {
    (( # == 0 )) || :die 'Expected no arguments.'

    # Since function-based recursion is prohibitively expensive, implement such
    # recursion as a depth-first search (DFS) of overlays beginning at the
    # following tree roots (in order):
    #
    # * The system-wide root zeshy overlay.
    # * The user-specific root zeshy overlay, if found.
    ::zeshy.compile_overlay_root "${ZY_OVERLAY_DIR}"
    if [[ -d ${ZY_USER_OVERLAY_DIR} ]] {
        ::zeshy.compile_overlay_root "${ZY_USER_OVERLAY_DIR}"
    }
}

# :void ::zeshy.compile_digest()
function ::zeshy.compile_digest() {
    (( # == 0 )) || :die 'Expected no arguments.'
    :stdout.echo_if_debug 'Compiling "'${ZY_USER_DIGEST_FILENAME}'".'

    # Make the directory containing such digest file, if needed. See
    # :set_string_to_path_dirname() for further details.
    command mkdir -p -- "${ZY_USER_DIGEST_FILENAME:h}"

    # Compile zeshy's current user digest file.
    zcompile -c --\
        "${ZY_USER_DIGEST_FILENAME}" "${ZY__FUNCTION_NAMES[@]}"
}

# ....................{ MAIN                              }....................
# Recompile zeshy's user digest file.
::zeshy.compile

return
# --------------------( WASTELANDS                        )--------------------
# ::script_zsh.source "${ZY_SCRIPT_DIRNAME}"/**/*.zy~"${ZY_SCRIPT_DIRNAME}"/30-macro/*.z
# local -a yumyum; yumyum=( "${ZY_SCRIPT_DIRNAME}"/**/*.zy )
# print -l "${yumyum}"
