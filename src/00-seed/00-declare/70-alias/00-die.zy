#!/usr/bin/env zsh
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2017 by Cecil Curry.
# See "LICENSE" for additional details.

:parcel <<'/---'
Handle *alias declarer exception handlers* (i.e., functions throwing exceptions
resulting from alias declarers).
/---

# ....................{ EXCEPTIONS                         }....................
# Manually handle arguments passed to this function, as the aliases defining the
# types of these arguments (e.g., :str()) have yet to be defined.
:func_ ':void ::alias.die(:str exception_message) [is_args_unhandled]' :func{
    #!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    # Synchronize with ::func.die().
    #!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    (( # == 1 )) || :die 'Expected one exception message.'
    local exception_message
    exception_message=$(::alias.label)${1}

    # If this prototype has been declared, append this prototype.
    if (( ${#ZY__ALIAS_PROTOTYPE-} )) {
        local alias_prototype=${ZY__ALIAS_PROTOTYPE} alias_type

        # For readability, indent this prototype and capitalize this type.
        ::Str.indent alias_prototype
        ::alias_current.Type_capitalized alias_type

        # This suffix.
        exception_message+=$'\n\n'${alias_type}' alias prototype:'$'\n'${alias_prototype}
    }

    # Throw this exception.
    :die ${exception_message}
}:func <<'/---'
Throw an exception with the passed message, presumably detailing a syntactic or
semantic error in the currently declared alias prototype. This private
exception handler is intended to be called _only_ by ::alias().

== Message Format ==

The passed message should be formatted as a lowercase sentence fragment (e.g.,
`prototype invalid`), which this function then:

* Prefixes by `Parcel ={parcel_name} alias ${alias_name}() `, where:
** `${parcel_name}` is the name of the parcel containing such alias.
** `${alias_name}` is the first alias name such prototype declares. (For
   simplicity, any subsequent alias name such prototype declares is ignored.)
* Suffixes by such prototype.
/---

:func_ ':void ::alias.die_of_prototype() [is_args_unhandled]' :func{
    #!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    # Synchronize with ::func.die_of_prototype().
    #!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    (( ! # )) || :die 'Expected no arguments.'
    ::pcre.die_of_subject_not_matching\
        "${ZY__CALLABLE_PROTOTYPE_PCRE}"\
        "${ZY__ALIAS_PROTOTYPE}"\
        "$(::alias.label)prototype "
}:func <<'/---'
Throw an exception detailing a syntactic error in the currently declared
alias prototype.

This private exception handler is intended to be called _only_ by :alias().
/---

#FIXME: Create a similar ::func.die_of_redefinition() in zy:../*-func/die.zy.
:func_ ':void ::alias.die_of_redefinition(:str alias_expansion) [
    is_args_unhandled]' :func{
    (( # == 1 )) || :die 'Expected one alias expansion.'
    local alias_expansion=${1} alias_type alias_definition

    # Such type, capitalized for readability.
    ::alias_current.Type_capitalized alias_type

    # Such expansion, indented for readability.
    ::Str.indent alias_expansion

    # Throw such exception.
    ::alias.die\
        'already defined.'$'\n\n'${alias_type}' alias definition:'$'\n'${alias_expansion}
}:func <<'/---'
Throw an exception resulting from the currently declared alias erroneously
redefining an existing alias with the passed expansion.

This private exception handler is intended to be called _only_ by ::alias().
/---

# ....................{ GETTERS                            }....................
:func_ ':str ::alias.label() [is_args_unhandled]' :func{
    #!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    # Synchronize with ::func.label().
    #!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    (( ! # )) || :die 'Expected no arguments.'
    local alias_label
    alias_label=$(:parcel.label)'alias '

    # If such prototype declares one name, prepend this label by this name.
    if (( ${#ZY__ALIAS_NAMES-} == 1 )) {
        alias_label+=${ZY__ALIAS_NAMES[1]}'() '
    # Else if such prototype declares multiple names, prepend this label by all
    # such names.
    } elif (( ${#ZY__ALIAS_NAMES-} > 1 )) {
        alias_label+='{'${ZY__ALIAS_NAMES[*]}'}() '
    # Else, such prototype has not yet been parsed. Ignore such names.
    }

    # Get such label.
    print -r -- "${alias_label}"
}:func <<'/---'
Get a human-readable string describing the alias being currently defined.
/---

# ....................{ LETTERS                            }....................
:func_ ':void ::alias_current.Type_capitalized(
    :Str type_capitalized_name) [is_args_unhandled]' :func{
    (( # == 1 )) || :die 'Expected one string name.'
    :Str.set "${1}" "${(C)ZY__ALIAS_TYPE[1]-}${ZY__ALIAS_TYPE[2,-1]-}"
}:func <<'/---'
Set the passed string variable to the human-readable capitalized type of the
currently declared alias (e.g., `Command`, `Command Filetype`, `Word`).

This private setter is intended to be called _only_ by exception handlers called
by ::alias().
/---
