#!/usr/bin/env zeshy
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2017 by Cecil Curry.
# See "LICENSE" for additional details.

:parcel <<'/---'
`zsh` exceptions.
/---

#FIXME: Add examples.

# ....................{ EXCEPTIONS                         }....................
:func.doc <<'/---'
:void die_unless_in_always(
    ?:str error_message="Not in an \"always :\{...\}:\" block (or \$\{TRY_BLOCK_ERROR\} ${TRY_BLOCK_ERROR} not in [0, 255] and hence an invalid exit status).")

Throw an exception with the passed message unless this function was transitively
called in an `always {...}` block.

See is_in_always() for further details.
/---
function die_unless_in_always() {
    die_unless_args_0_to_1 'Expected optional error message.'
    is_in_always :or :die\
        "${1-Not in an \"always :\{...\}:\" block (or \$\{TRY_BLOCK_ERROR\} ${TRY_BLOCK_ERROR--1} not in [0, 255] and hence an invalid exit status).}"
}

:func.doc <<'/---'
void die_evaluated(string evaluation)

Throw an exception whose error message is the result of dynamically evaluating
the passed string in a *double-quoted context* (i.e., as
`eval 'die "'${evaluation}'"'`). Since the result of dynamically evaluating a
string containing no *process substitutions* (i.e., `$(...)` expressions) is the
same string, this function is typically only passed single-quoted strings
containing such substitutions -- namely, substitutions of functions dependent on
the current terminal width.

== Terminal Width ==

The width available to each line of exception messages (typically) differs from
the width available to each line of normal output (i.e., the current terminal
width). Assuming default user-configurable styles, :die() prints exception
messages in frames fabricated with box-drawing glyphs, implying no line of such
message may be longer than the current terminal width minus the lengths of the
left and right borders of such frames.

For readability, this function evaluates the passed string with a terminal width
temporarily set to such length. Consider the following erroneous call to :die():

.Incorrect Terminal Width under :die()
==========================================
[source]
------------------------------------------
>>> :die "$(print_runnable info)"
┎┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈• :die() •┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┒
┋ ┃┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈• Command          ┋
┋ info •┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┃                          ┋
┋ /usr/bin/info                                                                                 ┋
┖┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┚
------------------------------------------
==========================================

The resulting message is plainly malformed. Consider calling this function with
the same string, single-quoted rather than double-quoted to delay execution of
such substitution until this function has temporarily overriden the current
terminal width with the restricted length:

.Correct Terminal Width under die_evaluated()
==========================================
[source]
------------------------------------------
>>> die_evaluated '$(print_runnable info)'
┎┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈• die_evaluated() •┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┒
┋ ┃┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈• Command info •┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┃ ┋
┋ /usr/bin/info                                                                                  ┋
┖┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┚
------------------------------------------
==========================================
/---
function die_evaluated() {
    # Validate sanity.
    die_unless_arg 'Expected one evaluatable string.'
    :str evaluation="${1}" message

    # If get_exception_message_width() has been defined by this point,
    # evaluate this string under a terminal width restricted accordingly.
    if { :func.is get_exception_message_width } {
        COLUMNS="$(get_exception_message_width)"\
            eval 'message="'${evaluation}'"'
    # Else, evaluate this string as is.
    } else {
        eval 'message="'${evaluation}'"'
    }

    # Throw such exception with such evaluated message.
    :die "${message}"
}

# ....................{ EXCEPTIONS ~ exception             }....................
:func.doc <<'/---'
void die_unless_exception(string error_message = "No exception thrown.")

Throw an exception with the passed message unless an uncaught exception was
thrown by the `{...}` block preceding the `always {...}` block calling this
function. See is_exception() for further details.
/---
function die_unless_exception() {
    die_unless_args_0_to_1 'Expected optional error message.'
    is_exception :or :die "${1-No exception thrown.}"
}

:func.doc <<'/---'
void die_unless_exception_caught(string error_message = "No exception caught.")

Throw an exception with the passed message unless an exception was caught by the
`always {...}` block calling this function. See is_exception_caught() for
further details.
/---
function die_unless_exception_caught() {
    die_unless_args_0_to_1 'Expected optional error message.'
    is_exception_caught :or :die "${1-No exception caught.}"
}

# ....................{ TESTERS                            }....................
:func.doc <<'/---'
[status: bool] is_in_always()

Report success if this function was transitively called in an `always {...}`
block.

== Caveats ==

*While reliable, this function is _not_ infallible.* Since list and string
globals ${zsh_eval_context} and ${ZSH_EVAL_CONTEXT} fail to contextualize the
execution of code in `always {...}` blocks, this function defers to integer
global ${TRY_BLOCK_ERROR} as a proxy for such context.

If ${TRY_BLOCK_ERROR} is undefined or less than zero, this function _cannot_
have been called in a sane `always {...}` block. On entering such a block, `zsh`
_always_ sets such integer to the exit status of the last command in the prior
block and hence an integer in [0, 255]. Likewise, on exiting such block, `zsh`
returns such integer as such block's exit status and reverts such integer to its
default value of -1.

Under ideal conditions, ${TRY_BLOCK_ERROR} is defined to be greater than or
equal to zero _only_ when expanded from an `always {...}` block. However, such
global is user-alterable; a defective `always {...}` block could reset such
global to a negative integer, in which case this function would erroneously
report failure despite being called in an `always {...}` block. Fortunately,
such edge case remains unlikely, rendering this concern (mostly) moot.
/---
function is_in_always() {
    die_if_args
    (( ${TRY_BLOCK_ERROR--1} >= 0 ))
}

# ....................{ TESTERS ~ exception                }....................
:func.doc <<'/---'
[status: bool] is_exception()

Report success if an uncaught exception was thrown by the `{...}` block
preceding the `always {...}` block calling this function. Since
throw_exception() sets string global ${EXCEPTION} to such exception's error
message and catch_exception() unsets such global, the existence of such global
implies such condition.
/---
function is_exception() {
    # For efficiency, inline such test. See :Var.is() for further details.
    die_if_args
    (( ${+EXCEPTION} ))
}

:func.doc <<'/---'
[status: bool] is_exception_caught()

Report success if an exception was caught by the `always {...}` block calling
this function. Since catch_exception() sets string global ${CAUGHT} to such
exception's error message and ::exception.print_if_uncaught() unsets such global,
the existence of such global implies such condition.
/---
function is_exception_caught() {
    # For efficiency, inline such test. See :Var.is() for further details.
    die_if_args
    (( ${+CAUGHT} ))
}

:func.doc <<'/---'
[status: bool] is_exception_stored()

Report success if an uncaught exception was thrown by the `{...}` block
preceding the `always {...}` block calling this function _and_ all metadata
previously associated with such exception by ::exception.store() still
exists. Specifically, report success if the following variables are still
defined: ${EXCEPTION}, ${ZY__EXCEPTION_STATUS}, ${ZY__EXCEPTION_FUNCSTACK},
${ZY__EXCEPTION_FUNCFILETRACE}, and ${ZY__EXCEPTION_FUNCSOURCETRACE}.
/---
function is_exception_stored() {
    # While we could also verify the correct types for such globals, such
    # zealousness smacks of overkill. (And not the good, heavy metal kind.)
    die_if_args

    #FIXME: Odd. Very well, stupidly silly. Just define a single :Var.is()
    #function accepting multiple arguments. Excise :is_vars()!
    :is_vars\
        EXCEPTION\
        ZY__EXCEPTION_STATUS\
        ZY__EXCEPTION_FUNCSTACK\
        ZY__EXCEPTION_FUNCFILETRACE\
        ZY__EXCEPTION_FUNCSOURCETRACE
}

# ....................{ GETTERS                            }....................
:func.doc <<'/---'
string get_exception_message()

Get the error message associated with the uncaught exception thrown by the
`{...}` block preceding the `always {...}` block calling this function.
Specifically, get the value of string global ${EXCEPTION}.
/---
function get_exception_message() {
    die_if_args
    die_unless_exception
    :stdout.echo "${EXCEPTION}"
}

:func.doc <<'/---'
string get_exception_caught_message()

Get the error message associated with the exception caught by the `always {...}`
block calling this function.  Specifically, get the value of string global
${CAUGHT}.
/---
function get_exception_caught_message() {
    die_if_args
    die_unless_exception_caught
    :stdout.echo "${CAUGHT}"
}

# ....................{ THROWERS                           }....................
:func.doc <<'/---'
void throw_exception_caught()

Rethrow the exception caught by the `always {...}` block calling this function.
Such exception will be rethrown with the same error message but _not_ exit
status or call stack associated with the original exception.
/---
function throw_exception_caught() {
    # Validate sanity.
    die_if_args
    die_unless_in_always
    die_unless_exception_caught

    #FIXME: While this works, it also discards the call stack associated with
    #the original exception. This is fixable -- but are we motivated?

    # Rethrow the currently caught exception.
    throw_exception "${CAUGHT}"
}

# ....................{ CATCHERS                           }....................
:func.doc <<'/---'
void catch_exception()

Catch any uncaught exception thrown by the `{...}` block preceding the
`always {...}` block calling this function if such an exception exists or throw
an exception otherwise. See catch_exception_if_found() for further details.
/---
function catch_exception() {
    die_if_args
    catch_exception_if_found :or die_unless_exception
}

:func.doc <<'/---'
[status: bool] catch_exception_if_found()

Catch any uncaught exception thrown by the `{...}` block preceding the
`always {...}` block calling this function if such an exception exists or report
failure otherwise. ``Catching'' an exception implies the following:

* Setting string global ${CAUGHT} to ${EXCEPTION}, the error message thrown with
  such exception.
* Undefining all globals previously associated with such exception by
  ::exception.store() (e.g., ${EXCEPTION}, ${ZY__EXCEPTION_STATUS}).
  See such function for further details.
* Setting ${TRY_BLOCK_ERROR} to 0 prevent such `always {...}` block from
  propagating such exception back up the call stack.

After calling this function, no exception will appear to have been thrown (e.g.,
is_exception() and is_exception_stored() will report failure). See
throw_exception() for further details.
/---
function catch_exception_if_found() {
    # Validate sanity.
    die_if_args
    die_unless_in_always

    # If any exception was thrown, report success from both the current function
    # and "always {...}" block *AND* catch such exception.
    is_exception :and {
        # Catch this exception.
        :str.global CAUGHT="${EXCEPTION}"

        #FIXME: Ideally, we would expand alias :Var.undefine() here.
        #Unfortunately, such alias is currently defined somewhat too
        #zealously. Rename such alias to :Var_constant.undefine() and define
        #a *NEW* alias :Var.undefine() expanding only to call unset() as
        #below (and hence accepting multiple arguments!).

        # Clear such exception by unsetting all globals previously set by
        # ::exception.store() *AFTER* catching such exception.
        unset\
            EXCEPTION\
            ZY__EXCEPTION_STATUS\
            ZY__EXCEPTION_FUNCSTACK\
            ZY__EXCEPTION_FUNCFILETRACE\
            ZY__EXCEPTION_FUNCSOURCETRACE

        # Report success from this "always {...}" block *AFTER* successfully
        # completing all prior logic.
        TRY_BLOCK_ERROR="${ZY_STATUS_SUCCESS-0}"
    }
}

:func.doc <<'/---'
<globbable> [status: bool] catch_exception_matching_glob_if_found(
    string error_message_glob)

Catch any uncaught exception thrown by the `{...}` block preceding the
`always {...}` block calling this function if such an exception was thrown with
an error message matching the passed glob or report failure otherwise. This
function is (effectively) equivalent to catch() in conventional `zsh`. See
catch_exception_if_found() for further details.
/---
function catch_exception_matching_glob_if_found() {
    # Validate sanity.
    die_unless_arg 'Expected one glob.'
    die_unless_in_always

    # If an uncaught exception matches such glob, catch such exception.
    :Var.is EXCEPTION and
    :str.^=.is "${EXCEPTION}" "${1}" :and
        catch_exception_if_found
}
