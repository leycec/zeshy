#!/usr/bin/env zsh
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2013 by Cecil Curry.
# See "COPYING" for additional details.

declare_parcel_as_script_with_stdin <<'-/-'
Handle *USE flags* (i.e., Gentoo-specific lowercase words instructing ebuilds to
enable support for the corresponding feature). 
-/-

# ....................{ PRINTERS                           }....................
declare_function_with_stdin <<'/---'
void print_use_flag(string use_flag_name1, ...)

Print a human-readable synopsis of the passed USE flags.
/---
function print_use_flag() {
    # Validate sanity.
    die_unless_args 'Expected one or more USE flags.'

    # Prefer "euse" to manually grepping files.
    {
        print_message_heading 'USE flags'

        string use_flag
        for    use_flag ("${@}") {
            if { is_pathable euse } {
                command euse --info  "${use_flag}"
            } else {
                grep_path ":${use_flag} - "\
                    "$(get_portage_home)/profiles/use."{,local.}desc
            }
        }
    } | page_stdin
}

declare_function_with_stdin <<'/---'
void print_use_flags_matching_grep(string grep_pattern)

Print a human-readable synopsis of all USE flags matching the passed case-
insensitive `grep` pattern.
/---
function print_use_flags_matching_grep() {
    # Validate sanity.
    die_unless_arg 'Expected one grep pattern.'

    # Grep for such flags.
    {
        print_message_heading 'USE flags'
        grep_path "$(get_portage_home)/profiles/use."{,local.}desc "${1}"
    } | page_stdin
}

# --------------------( WASTELANDS                         )--------------------
