#!/usr/bin/env zsh
# ====================[ string                             ]====================
#
# --------------------( SYNOPSIS                           )--------------------
# Zeshy String, handling core string functionality.

# ....................{ EXCEPTIONS                         }....................
# void die_unless_string(
#   char *variable_name, char *error_message = 'not a string')
#
# Throw an exception unless the variable with the passed name is a string.
die_unless_string() {
    die_unless_one_or_two_args\
        'expected one variable name and optional error message'
    is_string "${1}" or die "${2:-\"${1}\" not a declared string}"
}

#FIXME: Rename die_unless_character().
# void die_unless_char(
#   char *variable_name, char *error_message = 'not a string')
#
# Throw an exception unless the variable with the passed name is a single-
# character string.
die_unless_char() {
    die_unless_one_or_two_args\
        'expected one variable name and optional error message'
    is_char "${1}" or die "${2:-\"${1}\" not a declared character}"
}

# ....................{ TESTS                              }....................
# bool is_string(char *variable_name)
#
# Return true if the variable with the passed name is a string.
is_string() {
    die_unless_one_arg 'expected one variable name'
    is $(get_type "${1}") == 'scalar'* si
}

#FIXME: Rename is_character().
# bool is_char(char *variable_name)
#
# Return true if the variable with the passed name is a single character.
is_char() {
    die_unless_one_arg 'expected one variable name'
    is_string "${1}" and (( $(get_length "${(P)1}") == 1 ))
}

# ....................{ GETTERS ~ length                   }....................
# int get_length(char *string)
#
# Get the length of the passed string in single-byte characters. If such string
# contains multibyte characters (e.g., UTF-8-encoded Unicode), this may not be
# what you expect.
get_length() {
    die_unless_one_arg 'expected one string'
    sputter ${#1}
}

# int get_multibyte_length(char *string)
#
# Get the length of the passed string in multibyte characters.
get_multibyte_length() {
    die_unless_one_arg 'expected one string'
    sputter ${(m)#1}
}

# ....................{ GETTERS ~ lines                    }....................
# Curiously, Zsh expands parameter flag "(f)" to scalar strings rather single
# element lists when the parameter contains no newlines. Since this is probably
# never what anyone wants, this is probably a bug... To subdue this bug, always
# split the parameter into an explicit list prior to use: e.g.,
#
#     # Bad. Never do this. If "${1}" contains no newline, this prints the first
#     # character rather than first line of the parameter.
#     print ${#${(f)1}}
#
#     # Good. Always do this. While cumbersome, there's no avoiding some cumber.
#     list lines; lines=( ${(f)1} )
#     print ${#lines}

# char *get_line(char *string, int line_integer = 1)
#
# Get the line corresponding to the passed line number from the passed string.
# If positive, this is the nth line from the beginning starting at 1. Conversely
# if negative, this is the nth line from the end starting at -1.
get_line() {
    # Localize passed arguments.
    die_unless_one_or_two_args\
        'expected one string and optional line number'
    integer line_integer=${2:-1}
    get_lines "${1}" ${line_integer} ${line_integer}
}

# char *get_first_lines(char *string, int last_line_integer = 1)
#
# Get the first n lines (defaulting to the first line) from the passed string.
get_first_lines() {
    die_unless_one_or_two_args\
        'expected one string and optional last line number'
    get_lines "${1}" 1 ${2:-1}
}

# char *get_last_lines(
#   char *string, int last_line_integer = get_line_length(string))
#
# Get the last n lines (defaulting to the last line) from the passed string.
get_last_lines() {
    die_unless_one_or_two_args\
        'expected one string and optional last line number'
    string text="${1}"
    integer line_length; line_length=$(get_line_length "${text}")
    get_lines "${text}" ${2:-${line_length}} ${line_length}
}

# char *get_lines(
#   char *string,
#   int first_line_integer = 1, last last_line_integer = get_line_length(string))
#
# Get the list of lines starting at the passed first line number (defaulting to
# the first line) and concluding at the passed last line number (defaulting to
# the last line) from the passed string.
get_lines() {
    # Localize passed arguments.
    die_unless_one_to_three_args\
        'expected one string and optional first and last line numbers'
    list lines; lines=( "${(f)1}" )
    integer line_length=${#lines}
    string first_line_integer=${2:-1} last_line_integer=${3:-${line_length}} 
    die_unless_integer "${first_line_integer}"
    die_unless_integer "${last_line_integer}"

    # Validate passed arguments. Negative line numbers are permissible.
    (( -line_length < first_line_integer && first_line_integer <= line_length )) or
        die "line ${first_line_integer} not in [-${line_length}, ${line_length}]"
    (( -line_length < last_line_integer && last_line_integer <= line_length )) or
        die "line ${last_line_integer} not in [-${line_length}, ${line_length}]"

    # Compare the passed line numbers based on whether they have the same sign.
    string error="first line ${first_line_integer} > last line ${last_line_integer}"
    if (( first_line_integer * last_line_integer > 0 ))
    then (( first_line_integer <= last_line_integer )) or die "${error}"
    else (( first_line_integer >= last_line_integer )) or die "${error}"
    fi

    # Reconstruct the desired lines, appending a newline onto each.
    print -l -- ${lines[${first_line_integer},${last_line_integer}]}
}

# char *get_line_length(char *string1, char *string2, ...)
#
# Get the total number of lines in the passed strings.
get_line_length() {
    die_unless_args 'expected at least one string'
    list lines; lines=( "${(f)@}" )
    sputter ${#lines}
}

#FIXME: I'm fairly sure that, as with the "(f)" flag above, the "(w)" flag
#erroneously operates on characters if there exists only one such word. Fix.
# ....................{ GETTERS ~ words                    }....................
# char *get_first_word(char *string, char *string2, ...)
#
# Get the first word from the passed string.
get_first_word() {
    die_unless_args 'expected at least one string'
    sputter "${*[(w)1]}"
}

# char *get_word(char *string, int word_integer = 1)
#
# Get the word corresponding to the passed word number from the passed string.
get_word() {
    die_unless_one_or_two_args\
        'expected one string and optional word number'
    sputter "${1[(w)${2:-1}]}"
}

# char *get_word_length(char *string, char *string2, ...)
#
# Get the number of words in the passed string, aggregating each quoted
# substring into one word. Hence, this is the Zsh-specific word length.
get_word_length() {
    die_unless_args 'expected at least one string'
    sputter ${#${(z)*}}
}

# char *get_word_length_unquoted(char *string1, char *string2, ...)
#
# Get the number of words in the passed string, *NOT* aggregating quoted
# substrings. Hence, this is the conventional document word length.
get_word_length_unquoted() {
    die_unless_args 'expected at least one string'
    sputter ${#${=*}}
}

# ....................{ WRITERS                            }....................
# char *suffix_if_not_already(char *string, char *suffix)
#
# Suffix the passed string with the passed suffix, if not already: e.g.,
#
#     >>> suffix_if_not_already '/habitat/failing/flailing/habits' '/'
#     /habitat/failing/flailing/habits/
suffix_if_not_already() {
    die_unless_two_args 'expected one string and one suffix'
    sputter "${1%${2}}${2}"
}

# char *tabulate(char *string, char *delimiter = ' ')
#
# Horizontally align the passed strings on the passed delimiter (defaulting to
# whitespace) into table columns.
tabulate() {
    die_unless_one_or_two_args 'expected one string and optional delimiter'
    if is_one_arg
    then sputter "${1}" | column -t
    else sputter "${1}" | column -t -s "${2}"
    fi
}

# ....................{ WRITERS ~ case                     }....................
#FIXME: Generalize such functions to accept one or more strings. Also, suffix
#with "_string" for safety (i.e., we might someday want a capitalize_string_list()
#function, in which case a capitalize_string() function sows confusion).
# char *capitalize_string(char *string)
#
# Capitalize the first letter of each word in the passed string: e.g.,
#
#     >>> capitalize_string "phase three: profit"
#     Phase Three: Profit
capitalize_string() {
    die_unless_one_arg 'expected one string'
    sputter "${(C)1}"
}

# char *lowercase_string(char *string)
#
# Convert the passed string to all lowercase_string: e.g.,
#
#     >>> lowercase_string "The Wind in the Pillows"
#     the wind in the pillows
lowercase_string() {
    die_unless_one_arg 'expected one string'
    sputter "${(L)1}"
}

# char *uppercase_string(char *string)
#
# Convert the passed string to all uppercase_string: e.g.,
#
#     >>> uppercase_string "The Pyre of My Loins"
#     THE PYRE OF MY LOINS
uppercase_string() {
    die_unless_one_arg 'expected one string'
    sputter "${(U)1}"
}

# ....................{ WRITERS ~ lines                    }....................
# char *number_lines(char *string)
#
# Prefix each line of the passed string with that line's number.
number_lines() {
    sputter "${@}" |
        run_with_options 'run_paged command nl' 'ZESHY_NL_OPTIONS'
}

#FIXME: Should probably be theme-specific.
# char *number_lines_highlighting(char *string, int highlight_line_integer)
#
# Prefix each line of the passed string with that line's number and highlight
# the passed line number. Since "nl" supports no highlighting, this function
# accepts no "nl"-specific options.
number_lines_highlighting() {
    # Localize passed arguments.
    die_unless_two_args 'expected one string and one line number'
    die_unless_positive_integer "${2}"
    string text="${1}"
    integer line_integer="${2}" line_length

    #FIXME: Actually, it's trivial to convert negative to positive line numbers.
    # Validate passed arguments. Negative line numbers are impermissible.
    line_length="$(get_line_length "${text}")"
#   utter "line length: ${line_length}" 
    (( 1 <= line_integer && line_integer <= line_length )) or
        die "line number ${line_integer} not in [1, ${line_length}]"

    # Number lines.
    sputter "${text}" | awk -vNRHI=${line_integer} '
# If any line other than the highlight line, print the current line number
# followed by the line.
NR != NRHI { printf("%6d%7s%s\n", NR, "", $0) }
# If the highlight line, print the current line number prefixed and suffixed by
# pleasing ASCII arrows. (See print_stack_trace() for similar logic.)
NR == NRHI {
    # Number of digits in the current line number.
    NRLEN = length(NR);
         if (NRLEN==1) {NRNOW = " <-~ " NR}
    else if (NRLEN==2) {NRNOW = "<-~ " NR}
    else if (NRLEN==3) {NRNOW = "<- " NR}
    else               {NRNOW = NR}       # if true, your string is horrid.
    printf("%s ~->   %s\n", NRNOW, $0)
}'
}

# ....................{ WRITERS ~ removers                 }....................
# char *remove_prefix(char *string, char *prefix)
#
# Remove the passed prefix from the passed string: e.g.,
#
#     >>> remove_prefix 'zealia brown' 'zeal'
#     ia brown
remove_prefix() {
    die_unless_two_args 'expected one string and one prefix'
    sputter "${1##${2}}"
}

# char *remove_suffix(char *string, char *suffix)
#
# Remove the passed suffix from the passed string: e.g.,
#
#     >>> remove_suffix 'august derleth' 'leth'
#     august der
remove_suffix() {
    die_unless_two_args 'expected one string and one suffix'
    sputter "${1%%${2}}"
}

# char *truncate_string(
#   char *string, int maximum_length,
#   char *suffix_if_exceeding_maximum_length = '')
#
# Truncate the passed string to the passed length, replacing the end of such
# string with the passed suffix (defaulting to the empty string) if the passed
# string exceeds such length: e.g.,
#
#     >>> truncate_string 'Fallujah' 7
#     Falluja
#     >>> truncate_string 'Fallujah' 7 '...'
#     Fall...
truncate_string() {
    # Localize and validate passed arguments.
    die_unless_two_or_three_args\
        'expected one string, one length, and optional suffix'
    die_unless_positive_integer "${2}"
    string text="${1}" suffix_if_exceeding_maximum_length="${3:-}"
    integer text_length="${#text}" maximum_length="${2}"

    # If the passed string requires no truncation, return the same string.
    (( text_length > maximum_length )) or {
        sputter "${text}"
        return
    }

    # If a suffix was passed and the passed string is at least as long as such
    # suffix, truncate by replacing the end of the string with such suffix.
    if is_nonempty "${suffix_if_exceeding_maximum_length}"; then
        integer suffix_length="${#suffix_if_exceeding_maximum_length}"
        if  (( maximum_length >  suffix_length )); then
            (( maximum_length -= suffix_length ))
#           print "maximum_length: ${maximum_length}"
            sputter\
                "${text[1,${maximum_length}]}${suffix_if_exceeding_maximum_length}"
            return
        fi
    fi

    # Otherwise, truncate without suffix replacement.
    sputter "${text[1,${maximum_length}]}"
}

# ....................{ WASTELANDS                         }....................
# char *number_lines_highlighting_line(
#   int highlight_line_integer,
#   char *string1, char *string2, ...)
#
# Prefix each line of the passed string with that line's number and highlight
# the passed line number. Since "nl" supports no highlighting, this function
# accepts no "nl"-specific options.
#number_lines_highlighting_line() {
#    # Localize passed arguments.
#    die_unless_at_least_two_args\
#        'expected one line number and one or more strings'
#    die_unless_integer "${1}"
#    integer highlight_line_integer="${1}" line_length
#    shift_one_arg
#    string text="${@}"

#FIXME: By definition, you only join a list to a string and split a list to a
#string. Hence, these function names are overly verbose. Rename
#join_join_list_to_string() to join_list_with() and
#join_join_list_to_string_prefixing_last_with() to join_list_prefixing_last_element_with().
#This produces much more legible code.

#FIXME: Right. join_list_to_string() is simply too brief. join_join_list_to_string() reads
#better.

    # Localize passed arguments.
#   die_unless_args\
#       'expected zero or more "nl" options and one string'
#   string text="${@[-1]}"
#   pop_one_arg

    # Prefix.
#   list nl; nl=( nl "${ZESHY_NL_OPTIONS[@]}" )
#   sputter "${text}" | {
#       if is_interactive
#       then "${nl[@]}" "${ZESHY_NL_OPTIONS_IF_INTERACTIVE[@]}"     "${@}"
#       else "${nl[@]}" "${ZESHY_NL_OPTIONS_IF_NON_INTERACTIVE[@]}" "${@}"
#       fi
#   }

    #FIXME: Implementable with pure-Zsh.
#   sputter "${1}" | head --lines="${2:-1}"

#   list newline_split; newline_split=( "${(s:\n:)1}" )
#   print "first line: ${newline_split[1]}"

# char *get_first_line(char *string)
#
# Get the first line from the passed string.
#get_first_line() {
#    die_unless_one_arg 'expected one string'
#   sputter "${1[(f)1]}"
#}

#   # Localize passed arguments.
#   (( # >= 1 && # <= 2 )) ||
#       die 'expected one string and optional first line number'
#   local text="${1}"
#   integer first_line_integer="${2}"

    # Prefix.
#   nl -v${first_line_integer} "${text}"

    # Bizarrely, the "(f)" no longer appears to behave as advertised on my
    # install. In fact, it no longer does anything at all. Consequently, we
    # perform a brute-force split on newlines. Ideally, this should be:
    #
    #     print "${#${(f)1}}"
    # 
    # Unfortunately, we can't even expand this to:
    # 
    #     print "${#${(s:\n:)1}}"
    # 
    # Doing so behaves correctly for all input *EXCEPT* that without newlines.
    # Consequently, we perform an even more brute-force split into a list.
#     >>> split_string_to_list '"Contumely Hard" Climaxes' array
#     >>> print "${array[1]}"
#     Contumely Hard
#    # If the caller passed no separator, default to word splitting.
#   if is_empty "${separator}"
#   then eval ${list_name}'=( ${(z)text} )'

#   is_set "${target_name_prefix}" or string ${target_name_prefix}
#   is_set "${target_name_suffix}" or string ${target_name_suffix}
#   is_set "${list_name}" or list "${list_name}"

#   die_unless_string "${target_name_prefix}"
#   die_unless_string "${target_name_suffix}"

#Compare the lengths of the original and resulting strings.
#   local string_matches_deleted="${string//${~glob}/}"
#   local glob_characters="${#glob//}"
#   print $(((${#string} - ${#string_matches_deleted}) / ${#glob}))
#   print "string_newlined: ${string_newlined}"

# --------------------( COPYRIGHT AND LICENSE              )--------------------
# The information below applies to everything in this distribution,
# except where noted.
#              
# Copyright 2007-2012 by Cecil Curry.
#   
#   http://www.raiazome.com
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.
