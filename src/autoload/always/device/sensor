#!/usr/bin/env zsh
# ====================[ sensor                             ]====================
#
# --------------------( SYNOPSIS                           )--------------------
# Zeshy Sensor, handling CPU, GPU, and motherboard heat and fan speed sensors.

# ....................{ CONFIGURERS                        }....................
# void configure_sensors(void)
#
# Configure "lm_sensors" and hence the set of sensor-specific kernel modules
# exposed to the "sensors" command run by print_sensors().
configure_sensors() {
    # Validate environment sanity.
    die_if_args
    die_unless_installed sensors-detect '"lm_sensors" not installed'
    die_unless_installed pwmconfig      '"lm_sensors" not installed'
    die_unless_superuser
    die_unless_interactive

    # If the system is under moderate load, require the user confirm his or her
    # desire to continue. "pwmconfig" temporarily turns off *ALL* fans, which
    # could result in hardware damage should the CPU attempt to soldier on.
    float processor_load
    processor_load="$(get_processor_load_as_one_minute_normalized_average)"
    if (( processor_load >= 0.5 )); then
        utter 'configuring sensors temporarily turns off fans.'
        utter 'system currently under load and hence may be damaged if turning off fans.'
        ask_bool_harder 'really turn off fans?' or return_false
    fi

    # Configure.
    utter_first_section 'configuring sensors...'
    sensors-detect
    utter_next_section  'configuring fans...'
    pwmconfig
}

# ....................{ PRINTERS                           }....................
# void print_sensors(void)
#
# Print a human-readable profile of all sensors the current machine exposes.
print_sensors() {
    die_if_args

    # Prefer "sensors" to "acpi". The former is considerably more general.
    if is_installed sensors
    then run_paged  sensors
    # Since "acpi" fails to output a helpful profile under many machines, inform
    # the user of the fact.
    elif is_installed acpi
    then
        interactively utter '"lm_sensors" not installed; reverting to "acpi"...'
        acpi --thermal
    # Otherwise, throw an exception.
    else die 'neither "lm_sensors" or "acpi" installed'
    fi
}

# --------------------( COPYRIGHT AND LICENSE              )--------------------
# The information below applies to everything in this distribution,
# except where noted.
#
# Copyright 2007-2012 by Cecil Curry.
#
#   http://www.raiazome.com
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.
