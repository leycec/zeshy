#!/usr/bin/env zsh
# ====================[ convert                            ]====================
#                     [ Time-stamp: "2009-04-18 19:29:36 leycec" ]
#
# --------------------( SYNOPSIS                           )--------------------
# Zeshy ImageMagick.

# ....................{ QUALITY REDUCTION                  }....................
# Note: ImageMagick supports quality reduction for only the following filetypes:
# JPEG (".jpg"), MIFF (".miff"), and PNG (".png").

# void imagemagick_reduce_images_quality(
#   char *source_dirname = PWD,
#   char *target_dirname_prefix = source_dirname,
#   char *target_filename_prefix = 'low_quality-',
#   int quality_percentage = 50)
#
# Recompress all images in the passed source directory to images in the passed
# target directory with the same basenames but prefixed with the passed prefix
# and with reduced compression quality corresponding to the passed quality as a
# percentage from 1 to 100: e.g.,
#
#     # Recompress all images in the current directory.
#     >> imagemagick_reduce_images_quality
#
#     # Recompress all images in directory ~/images/ to ~/images/small/ with
#     # filenames prefixed by "small__" at 75% quality.
#     >> imagemagick_reduce_images_quality ~/images/ ~/images/small/ small__ 75
imagemagick_reduce_images_quality() {
    die_unless_at_most_four_args\
        'expected optional source dirname, target dirname, '\
        'target filename prefix, and quality from 1 to 100'

    # Validate passed arguments. 
    string source_dirname="${1:-${PWD}}"
    string\
        target_dirname="${2:-${source_dirname}}"\
        target_filename_prefix="${3:-low_quality-}"
    integer quality_percentage="${4:-50}"

    # Validate passed arguments. 
    die_unless_dir "${source_dirname}"
    die_unless_dir "${target_dirname}"
    (( 1 <= quality_percentage && quality_percentage <= 100 )) or
        die "${quality_percentage} not a valid percentage"

    # Recompress all quality-respecting images in the passed source directory.
    string target_image
    for source_image ("${source_dirname}"/*.{jpg,miff,png}) {
        target_image="${target_dirname}/${target_filename_prefix}$(get_basename "${source_image}")"
        utter "reducing ${source_image} to ${target_image} at ${quality_percentage}% quality..."
        convert -quality "${quality_percentage}%"\
            "${source_image}" "${target_image}"
    }
}

# void imagemagick_reduce_image_quality(
#   char *source_filename,
#   char *target_filename = 'low_quality-' + source_filename,
#   int quality_percentage = 50)
#
# Recompress the passed source to target image with reduced compression quality
# corresponding to the passed quality as a percentage from 1 to 100, defaulting
# to 50 (i.e., reducing the target to half the quality of the source image).
imagemagick_reduce_image_quality() {
    die_unless_one_to_three_args\
        'expected one source image and optional target image and'\
        'quality from 1 to 100'

    # Localize and validate passed arguments. 
    string source_image="${1}"
    die_unless_file "${source_image}"
    string target_image="${2:-low_quality-${source_image}}"
    integer quality_percentage="${3:-50}"
    (( 1 <= quality_percentage && quality_percentage <= 100 )) or
        die "${quality_percentage} not a valid percentage"

    # Recompress.
    utter "reducing ${source_image} to ${target_image} at ${quality_percentage}% quality..."
    convert -quality "${quality_percentage}%"\
        "${source_image}" "${target_image}"
}

#FIXME: Obsolete.
    #FIXME: Generalize this for use elsewhere. Specifically, use the
    #deprioritize() function of "always/process".
    # If "nice" and/or "ionice" are installed, run "rsync" under whichever are
    # available to prevent CPU and hard-drive starvation.
#   is_installed nice   and rsync=(   nice -n19    "${rsync[@]}" )
#   is_installed ionice and rsync=( ionice -c2 -n7 "${rsync[@]}" )

# --------------------( COPYRIGHT AND LICENSE              )--------------------
# The information below applies to everything in this distribution,
# except where noted.
#              
# Copyright 2007-2012 by Cecil Curry.
#   
#   http://www.raiazome.com
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.
