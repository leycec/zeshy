#!/usr/bin/env zsh
# ====================[ X                                  ]====================
#
# --------------------( SYNOPSIS                           )--------------------
# Zeshy X, handling X11 and all forks thereof:
#       X.org, we're winking at you.

# ....................{ ALIASES                            }....................
interactive_alias xf='x_list_cached_fonts'
interactive_alias xfc='x_cache_font_dir'
interactive_alias startx='start_X'

# ....................{ SETUP                              }....................
# void setup_X(void)
#
# Initialize Zeshy-specific X.org integration.
setup_X() {
    #FIXME: "XAPPLRESDIR" now obsoleted by "XUSERFILESEARCHPATH". Delete the
    #former; retain the latter, after upgrading X.org.

    # Absolute path of the user-specific directory with Xt-driven (e.g., Xaw,
    # Motif) configuration files containing application resources.
    global_string XAPPLRESDIR="${XAPPLRESDIR:-${HOME}/.Xt}"
    global_string XUSERFILESEARCHPATH="${XUSERFILESEARCHPATH:-${XAPPLRESDIR}/%N}"
    make_dir_safely "$(get_dirname "${XUSERFILESEARCHPATH}")"
}

# void start_X(char *startx_option1, char *startx_option2, ...)
#
# Start the X.org server and a client session connecting to such server under
# the passed "startx" options. This function is a helper wrapping the "startx"
# command, itself a helper wrapping the "xinit" command, supporting:
#
# * Implicit logging of X.org standard error and output to "~/.xsession",
#   silently overwriting any prior such log.
start_X() {
    # ">!" truncates the log prior to writing.
    unify_output startx "${@}" >! "${HOME}/.xsession"
}

# ....................{ PRINTERS                           }....................
# char *print_x_keyboard_info(void)
#
# Print X11 keyboard information.
print_x_keyboard_info() {
    die_unless_no_args

    # Print all "setxkbmap" information, as even the most verbose of the
    # information output by this utility is informatively brief.
    utter 'displaying "setxkbmap" information...'
    setxkbmap -print -verbose 10

    # Print all display-specific "xset" information.
    print
    utter 'displaying "xset" information...'
    xset -q | grep '^Keyboard Control:$' --after-context=13
}

# char *print_x_screen_info(void)
#
# Print X11 display information, including the:
#
# * Current resolution.
# * Dots per inch (DPI).
print_x_screen_info() {
    die_unless_no_args

    # Print a subset of all "xdpyinfo" information, as most of the information
    # output by this utility is considerably more verbose than usually desired.
    utter 'displaying "xdpyinfo" information...'

    # Copy the output of "xdpyinfo" into a local variable.
    string display_info="$(xdpyinfo)"

    # Grep this variable for relevant lines.
    match_first_line ' *dimensions: ' "${display_info}"
    match_first_line ' *resolution: ' "${display_info}"
    match_first_line ' *depth of root window: ' "${display_info}"

    # Print all "xrandr" information, as most of the information output by this
    # utility is informatively brief enough to be of general use. 
    print
    utter 'displaying "xrandr" information...'
    xrandr

    # Print all display-specific "xset" information.
    print
    utter 'displaying "xset" information...'
    _print_x_dpms_info
    "xset -q | grep '^Colors:$' --after-context=1" 
}

# ....................{ PRINTERS ~ dpms                    }....................
# char *print_x_dpms_info(void)
#
# Print X11 Display Power Management Signaling (DPMS) and screen saver
# information.
print_x_dpms_info() {
    die_unless_no_args
    utter 'displaying X.org DPMS settings...'
    _print_x_dpms_info
}

_print_x_dpms_info() {
    die_unless_no_args
    string xset_output="$(xset -q)"
    print "${xset_output}" | grep '^Screen Saver:$'       --after-context=2
    print "${xset_output}" | grep '^DPMS (Energy Star):$' --after-context=2
}

# ....................{ SETTERS ~ booleans                 }....................
# void disable_x_dpms(void)
#
# Disable screen blanking (i.e., DPMS-managed monitor shutdown on inactivity).
# This is particularly helpful while enjoying media but should be manually re-
# enabled when done.
disable_x_dpms() {
    die_unless_no_args

    utter 'disabling X.org DPMS...'
    xset s off
    xset -dpms
    print_x_dpms_info
}

# void enable_x_dpms(void)
#
# Enable screen blanking (i.e., DPMS-managed monitor shutdown on inactivity).
enable_x_dpms() {
    die_unless_no_args
    
    utter 'enabling X.org screen blanking...'
    xset s on
    xset +dpms
    print_x_dpms_info
}

# ....................{ VIDEO ~ fonts                      }....................
# char *x_list_cached_fonts(void)
#
# List all currently cached fonts in the system-wide X11 font cache.
x_list_cached_fonts() {
    die_unless_no_args
    run_paged fc-cat -rv
}
 
# void x_font_dir_cache(char *dirname = PWD)
#
# Permanently add all fonts in the passed directory to the system-wide X11 font
# cache. This addition makes these fonts accessible to all subsequent "X11-
# aware" applications. This directory defaults to the current working directory,
# if not passed.
x_cache_font_dir() {
    die_unless_at_most_one_arg 'expected at most one font directory'

    # If non-empty, this function pushed the passed directory onto the stack and
    # must thus pop it from the stack prior to returning
    bool is_pushing_directory

    # Font directory to be cached.
    string font_dir
    if is_nonempty "${1}"; then
        font_dir="${1}"
        is_pushing_directory=1
        pushd "${font_dir}"
    else
        font_dir="${PWD}"
    fi
    font_dir="$(canonicalize_path "${font_dir}")"

    utter "preparing \"${font_dir}\" for caching..."
    mkfontscale
    mkfontdir

    utter "caching \"${font_dir}\"..."
    xset fp+ "${font_dir}"

    utter "updating X.org font list..."
    xset fp rehash

    # "fc-cache" is a dynamic alternative to the above. Older systems have no
    # "fc-cache" installed, however. For safety, do both.
    if is_installed fc-cache; then
        utter 'updating fontcache...'
        run_as_superuser fc-cache -v
    fi

    is_empty "${is_pushing_directory}" or popd
}

#FIXME: Obsolete.
#   global_string\
#       XUSERFILESEARCHPATH="${XUSERFILESEARCHPATH:-${HOME}/.Xt/%N}"

# --------------------( COPYRIGHT AND LICENSE              )--------------------
# The information below applies to everything in this distribution,
# except where noted.
#              
# Copyright 2007-2012 by Cecil Curry.
#   
#   http://www.raiazome.com
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.
