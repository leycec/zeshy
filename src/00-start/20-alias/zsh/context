#!/usr/bin/env zsh
# --------------------( LICENSE                            )--------------------
# Copyright 2007-2013 by Cecil Curry.
# See "COPYING" for additional details.

declare_parcel_as_script_with_stdin <<'/---'
Handle *`zsh` context* (i.e., lowercase strings describing the current shell
execution environment) aliases.
/---

# ....................{ GLOBALS                            }....................
declare_global_with_stdin string ZESHY_CONTEXT_ROOT='toplevel' <<'/---'
Value of ${zsh_eval_context} elements whose corresponding runnables run under
*root context* (i.e., run either by the current user directly at an interactive
command line _or_ at the top-level of a non-interactive shell script). See
is_context_root() for further details.
/---

# ....................{ GLOBALS ~ builtin                  }....................
declare_global_with_stdin string ZESHY_CONTEXT_EVALUATION='eval' <<'/---'
Value of ${zsh_eval_context} elements whose corresponding runnables are
*evaluated* (i.e., run by a call to builtin eval()).
/---

declare_global_with_stdin string ZESHY_CONTEXT_SCHEDULED='sched' <<'/---'
Value of ${zsh_eval_context} elements whose corresponding runnables are
*scheduled* (i.e., run by a prior call to builtin sched()).
/---

declare_global_with_stdin string ZESHY_CONTEXT_TRAP='trap' <<'/---'
Value of ${zsh_eval_context} elements whose corresponding runnables are *traps*
(i.e., triggered by a signal ``trapped'' by a prior call to builtin trap()).
This excludes trap functions, which `zsh` classifies as functions and hence
confers a context of ${ZESHY_CONTEXT_FUNCTION}.
/---

# ....................{ GLOBALS ~ command substitution     }....................
declare_global_with_stdin string\
    ZESHY_CONTEXT_COMMAND_SUBSTITUTION='cmdsubst' <<'/---'
Value of ${zsh_eval_context} elements whose corresponding runnables are *command
substitutions* (i.e., `\``- _or_ `$(`- and `$)`-delimited commands substituted
with the output of such commands).
/---

declare_global_with_stdin string\
    ZESHY_CONTEXT_ROOT_COMMAND_SUBSTITUTION="${ZESHY_CONTEXT_ROOT}:${ZESHY_CONTEXT_COMMAND_SUBSTITUTION}" <<'/---'
Prefix of ${ZSH_EVAL_CONTEXT} strings whose corresponding runnables are command
substitutions called under root context. See ${ZESHY_CONTEXT_ROOT} for further
details.
/---

# ....................{ GLOBALS ~ function                 }....................
declare_global_with_stdin string ZESHY_CONTEXT_FUNCTION='shfunc' <<'/---'
Value of ${zsh_eval_context} elements whose corresponding runnables are `zsh`
*functions*, including *trap functions* (i.e., traps implemented as functions
rather than through a call to builtin trap()).
/---

declare_global_with_stdin string\
    ZESHY_CONTEXT_FUNCTION_AUTOLOAD='loadautofunc' <<'/---'
Value of ${zsh_eval_context} elements whose corresponding runnables are the
*autoloading* of `zsh` functions from digest files or function scripts in the
current user's ${fpath}. Note that such value follows rather than replaces
${ZESHY_CONTEXT_FUNCTION} in ${zsh_eval_context} (e.g., autoloaded functions
called directly from the CLI have a scalar context of
`${ZESHY_CONTEXT_ROOT}:${ZESHY_CONTEXT_FUNCTION}:${ZESHY_CONTEXT_FUNCTION_AUTOLOAD}`
rather than `${ZESHY_CONTEXT_ROOT}:${ZESHY_CONTEXT_FUNCTION_AUTOLOAD}`).
/---

() {
    # Extract such glob to a temporary string local to permit use of proper
    # single quotations below. This is *NOT* merely an aesthetic decision: since
    # zsh treats adjacent single quotes as a single single quote, attempting to
    # expand ${ZESHY_CONTEXT_FUNCTION_GLOB} directly into the definitions of the
    # other globals below results in unmatchable glob expressions.
    :string function_glob_unprefixed="${ZESHY_CONTEXT_FUNCTION}'(|':${ZESHY_CONTEXT_FUNCTION_AUTOLOAD}')"

    declare_global_with_stdin string\
        ZESHY_CONTEXT_FUNCTION_GLOB="'${function_glob_unprefixed}" <<'/---'
Glob expression matching ${zsh_eval_context} elements whose corresponding
runnables are `zsh` functions. Since `zsh` assigns autoloaded and non-autoloaded
functions a different context, simple string literals do _not_ suffice to match
${zsh_eval_context} elements whose runnables are `zsh` functions. This glob thus
matches both autoloaded and non-autoloaded functions, eliminating such largely
irrelevant low-level distinctions. See ${ZESHY_CONTEXT_FUNCTION} and
${ZESHY_CONTEXT_FUNCTION_AUTOLOAD} for further details.
/---

    declare_global_with_stdin string\
        ZESHY_CONTEXT_CURRENT_FUNCTION_GLOB="(|*':')'${function_glob_unprefixed}" <<'/---'
Glob expression matching ${ZSH_EVAL_CONTEXT} strings suffixed by substrings
whose corresponding runnables are `zsh` functions, indicating the current
context to be a `zsh` function. See ${ZESHY_CONTEXT_FUNCTION_GLOB} for further
details.
/---

    declare_global_with_stdin string\
        ZESHY_CONTEXT_ROOT_FUNCTION_GLOB="'${ZESHY_CONTEXT_ROOT}:${function_glob_unprefixed}" <<'/---'
Glob expression matching ${ZSH_EVAL_CONTEXT} strings whose corresponding
runnables are `zsh` functions called under root context. See
${ZESHY_CONTEXT_ROOT} and ${ZESHY_CONTEXT_FUNCTION_GLOB} for further details.
/---

    declare_global_with_stdin string\
        ZESHY_CONTEXT_ROOT_FUNCTION_FUNCTION_GLOB="${ZESHY_CONTEXT_ROOT_FUNCTION_GLOB}':${function_glob_unprefixed}" <<'/---'
Glob expression matching ${ZSH_EVAL_CONTEXT} strings whose corresponding
runnables are `zsh` functions called by other `zsh` functions themselves called
under root context. See ${ZESHY_CONTEXT_ROOT} and ${ZESHY_CONTEXT_FUNCTION_GLOB}
for further details.
/---
}

# ....................{ TESTERS                            }....................
#FIXME: Add examples.
:declare_alias_simple '[status = :bool] is_in_function()'\
    ':is "${ZSH_EVAL_CONTEXT}" == '${ZESHY_CONTEXT_CURRENT_FUNCTION_GLOB}' :si' <<'/---'
Report success if the current shell context is a function (i.e., if this alias
was expanded directly within a function). This is largely useful in other
aliases, to determine whether or not such alias is being expanded from within a
function or some other shell construct.
/---

# ....................{ TESTERS ~ root                     }....................
:declare_alias_simple '[status = :bool] is_context_root()'\
    ':is "${ZSH_EVAL_CONTEXT}" == "'${ZESHY_CONTEXT_ROOT}'" :si' <<'/---'
Report success if the current shell context is the *root context* (i.e., if this
alias was expanded either by the current user directly at an interactive command
line _or_ at the top-level of a non-interactive shell script): e.g.,

.is_context_root()
==========================================
[source]
------------------------------------------
>>> :string on_demagoguery=\
...    "One Folk, One Realm, One Leader. Union with the unity of an insect
...     swarm. Knowledgeless understanding of nonsense and diabolism. And then
...     the newsreel camera had cut back to the serried ranks, the swastikas,
...     the brass bands, the yelling hypnotist on the rostrum. And here once
...     again, in the glare of his inner light, was the brown insectlike column,
...     marching endlessly to the tunes of this rococo horror-music. Onward Nazi
...     soldiers, onward Christian soldiers, onward Marxists and Muslims, onward
...     every chosen People, every Crusader and Holy War-maker. Onward into
...     misery, into all wickedness, into death!"
>>> is_context_root and get_string_line "${on_demagoguery}" 1
One Folk, One Realm, One Leader. Union with the unity of an insect
------------------------------------------
==========================================
/---

:declare_alias_simple '[status = :bool] is_context_cli()'\
    'is_shell_interactive and
        :is "${ZSH_EVAL_CONTEXT}" == "'${ZESHY_CONTEXT_ROOT}'" :si' <<'/---'
Report success if the current shell context is the *CLI* (i.e., this alias was
interactively expanded by the current user directly from the command line): e.g.,

.is_context_cli()
==========================================
[source]
------------------------------------------
>>> :string on_golden_ages=\
...    "We may not appreciate the fact; but a fact nevertheless it remains: we
...     are living in a Golden Age, the most gilded Golden Age of human history
...     — not only of past history, but of future history. For, as Sir Charles
...     Darwin and many others before him have pointed out, we are living like
...     drunken sailors, like the irresponsible heirs of a millionaire uncle. At
...     an ever accelerating rate we are now squandering the capital of metallic
...     ores and fossil fuels accumulated in the earth’s crust during hundreds
...     of millions of years. How long can this spending spree go on? Estimates
...     vary. But all are agreed that within a few centuries or at most a few
...     millennia, Man will have run through his capital and will be compelled
...     to live, for the remaining nine thousand nine hundred and seventy or
...     eighty centuries of his career as Homo sapiens, strictly on income."
>>> is_context_cli and get_string_line "${on_golden_ages}" -1
eighty centuries of his career as Homo sapiens, strictly on income.
------------------------------------------
==========================================
/---

:declare_alias_simple\
    '[status = :bool] is_context_cli_command_substitution()'\
    'is_shell_interactive and
        :is "${ZSH_EVAL_CONTEXT}" == "'${ZESHY_CONTEXT_ROOT_COMMAND_SUBSTITUTION}'" :si' <<'/---'
Report success if the current shell context is a *command substitution* (i.e.,
`\``- _or_ `$(`- and `$)`-delimited commands substituted with the output of such
commands) substituted directly from the CLI. See is_context_cli() for further
details: e.g.,

.is_context_cli_command_substitution()
==========================================
[source]
------------------------------------------
>>> :string on_ministry=\
...    "The Ministry of Truth, which concerned itself with news, entertainment,
...     education and the fine arts. The Ministry of Peace, which concerned
...     itself with war. The Ministry of Love, which maintained law and order.
...     And the Ministry of Plenty, which was responsible for economic affairs."
>>> $(if { is_context_cli_command_substitution }
...       :output_string 'get_string_line "${on_ministry}" -1'
...   } else {
...       :output_string 'get_string_line "${on_ministry}" 1'
...   })
And the Ministry of Plenty, which was responsible for economic affairs.
------------------------------------------
==========================================
/---

:declare_alias_simple '[status = :bool] is_context_cli_function()'\
    'is_shell_interactive and {
        if (( ${ZESHY_CONTEXT_IS_IGNORING_FUNCTION_CALL-0} )) {
            :is "${ZSH_EVAL_CONTEXT}" == '${ZESHY_CONTEXT_ROOT_FUNCTION_FUNCTION_GLOB}' :si
        } else {
            :is "${ZSH_EVAL_CONTEXT}" == '${ZESHY_CONTEXT_ROOT_FUNCTION_GLOB}' :si
        }
    }' <<'/---'
Report success if the current shell context is a `zsh` function called directly
from the CLI. For generality, this includes calls to *autoloaded functions*
(i.e., previously undefined functions ``autoloaded'' by such calls from digest
files or function scripts in the current user's ${fpath}). See is_context_cli()
for further details: e.g.,

.is_context_cli_function()
==========================================
[source]
------------------------------------------
>>> :string on_animality=\
...    "\"Animals don't behave like men,\" he said. \"If they have to fight,
...     they fight; and if they have to kill they kill. But they don't sit down
...     and set their wits to work to devise ways of spoiling other creatures'
...     lives and hurting them. They have dignity and animality.\""
>>> function on_dignity() {
...     is_context_cli_function and get_string_line "${on_animality}" 1
... }
>>> on_dignity
"Animals don't behave like men," he said. "If they have to fight,
------------------------------------------
==========================================

== Recognized Variables ==

This alias also internally references the following variables, which callers may
optionally set (ideally locally) to alter alias behavior:

* ${ZESHY_CONTEXT_IS_IGNORING_FUNCTION_CALL}, a boolean variable. If enabled,
  this alias instead returns success if the current shell context is a `zsh`
  function called by another `zsh` function called directly from the CLI. For
  simplicity, consider calling ignore_context_function_call() to locally enable
  such boolean. See grep_path_caseless_with_options() for example usage.
/---

# ....................{ IGNORERS                           }....................
:declare_alias_simple ':void ignore_context_function_call()'\
    ':bool ZESHY_CONTEXT_IS_IGNORING_FUNCTION_CALL='${ZESHY_BOOLEAN_TRUE} <<'/---'
Locally enable ${ZESHY_CONTEXT_IS_IGNORING_FUNCTION_CALL}, instructing
is_context_cli_function() to ignore the current function call and hence return
success if the current shell context is a `zsh` function called by another `zsh`
function called directly from the CLI. For sanity, _only_ expand this alias
within function bodies.
/---

# --------------------( WASTELANDS                         )--------------------
    # Duplicate the prior definition to provide proper single quotations. This is
    # *NOT* merely an aesthetic decision: since zsh treats adjacent single quotes as
    # a single single quote, attempting to interpolate
    # ${ZESHY_CONTEXT_FUNCTION_GLOB} directly into this definition results in a glob
    # expression that never matches.
    # See above.
#Instead, a
#glob expression 
#`zsh` functions in such elements.
#
#such low-level
#distinctions are generally unhelpful in practice
#
#While `zsh` declares autoloaded and
#non-autoloaded functions to have a different context, such low-level
#distinctions are generally unhelpful in practice. This glob matches both
#autoloaded and non-autoloaded functions, obviating
#these distinctions. See ${ZESHY_CONTEXT_FUNCTION} and
#${ZESHY_CONTEXT_FUNCTION_AUTOLOAD} for further details.

#           print "zsh context: ${ZSH_EVAL_CONTEXT}"
# ....................{ GLOBALS ~ root                     }....................
# Define root context globals *AFTER* all other context globals, as the former
# depends on the latter.
#Since this excludes
#autoloaded `zsh` functions, consider expanding
#${ZESHY_CONTEXT_ROOT_FUNCTION_GLOB} instead. 

# See such global for further details.

#String assigned to ${zsh_eval_context} elements whose corresponding runnables
#run under *root context*. See is_context_root() for further details.
#String assigned to ${zsh_eval_context} elements whose corresponding runnables
#are `zsh` functions.
#Value of ${zsh_eval_context} elements signifying `zsh` functions.

#>>> is_context_root and print_string\
#...     "Facts do not cease to exist because they are ignored."
#Facts do not cease to exist because they are ignored.
#(i.e., either directly by the current user from an
#interactive command line or by top-level of a non-interactive shell script).
